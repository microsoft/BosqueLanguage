//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

entity Queue<T> provides Object, Expandoable<T> {
    hidden static s_size(q: Queue<T>): Int # queue_size
    hidden static s_unsafe_front(q: Queue<T>): T # queue_unsafe_front
    hidden static s_unsafe_at(q: Queue<T>, idx: Int): T # queue_unsafe_at
    hidden static s_unsafe_push(q: Queue<T>, v: T): Queue<T> # queue_unsafe_push
    hidden static s_unsafe_pop(q: Queue<T>): Queue<T> # queue_unsafe_pop

    hidden static s_toList(q: Queue<T>, lp: List<T>, idx: Int): List<T> {
        if(idx == Queue<T>::s_size(q)) {
            return lp;
        }
        else {
            let val = Queue<T>::s_unsafe_at(q, idx);
            return Queue<T>::s_toList(q, List<T>::s_unsafe_push(lp, val), idx + 1);
        }
    }

    method size(): Int {
        return Queue<T>::s_size(this);
    }

    method empty(): Bool {
        return Queue<T>::s_size(this) == 0;
    }

    method toList(): List<T> {
        return Queue<T>::s_toList(this, List<T>@{}, 0);
    }

    method dequeue(): T, Queue<T>
        requires release Queue<T>::s_size(this) != 0;
    {
        return Queue<T>::s_unsafe_front(this), Queue<T>::s_unsafe_pop(this);
    }

    static idequeue(ref q: Queue<T>): T 
        requires release Queue<T>::s_size(q) != 0;
    {
        let res = Queue<T>::s_unsafe_front(this);
        q = Queue<T>::s_unsafe_pop(q);

        return res;
    }

    method peek(): T
        requires release Queue<T>::s_size(this) != 0;
    {
        return Queue<T>::s_unsafe_front(this);
    }

    method tryPeek(): T? {
        return Queue<T>::s_size(this) != 0 ? Queue<T>::s_unsafe_front(this) : none;
    }

    method lookahead(k: Int): T
        requires release k < Queue<T>::s_size(this);
    {
        return Queue<T>::s_unsafe_at(this, k);
    }

    method tryLookahead(k: Int): T? {
        return k < Queue<T>::s_size(this) ? Queue<T>::s_unsafe_at(this, k) : none;
    }

    method rest(): Queue<T>
        requires release Queue<T>::s_size(this) != 0;
    {
        return Queue<T>::s_unsafe_pop(this);
    }

    static irest(ref q: Queue<T>)
        requires release Queue<T>::s_size(q) != 0;
    {
        q = Queue<T>::s_unsafe_pop(q);
        return none;
    }

    method enqueue(v: T): Queue<T> {
        return Queue<T>::s_unsafe_push(this, v);
    }

    static ienqueue(ref q: Queue<T>, v: T) {
        q = Queue<T>::s_unsafe_push(q, v);
        return none;
    }
}
