//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

__internal entity List<T> provides Object, Expandoable<T>, APIType when T grounded APIType {
    function ints(start: Int, end: Int): List<Int> {
        check start <= end;

        return ListOps::s_range_int(start, end);
    }

    function nats(start: Nat, end: Nat): List<Nat> {
        check start <= end;

        return ListOps::s_range_nat(start, end);
    }

    method index(): List<[Nat, T]> {
        return ListOps::s_zip_index<T>(this, ListOps::s_size<T>(this));
    }

    method zip<U=?>(l: List<U>): List<[T, U]> {
        check ListOps::s_size<T>(this) == ListOps::s_size<U>(l);

        return ListOps::s_zip<T, U>(this, l, ListOps::s_size<T>(this));
    }

    ////
    //Public interface
    ////

    function fill(k: Nat, val: T): List<T> {
        return ListOps::s_fill<T>(k, val);
    }

    function concat(...ll: List<List<T>>): List<T> {
        if(ListFlatOps::s_empty<List<T>>(ll)) {
            return List<T>@{};
        }
        else {
            let count = ListOps::s_size<List<T>>(ll);
            if(count == 1) {
                return ListOps::s_safe_get<List<T>>(ll, 0n);
            }
            elif(count == 2) {
                return ListOps::s_concat2<T>(ListOps::s_safe_get<List<T>>(ll, 0n), ListOps::s_safe_get<List<T>>(ll, 1n));
            }
            elif(count == 3) {
                let fl = ListOps::s_concat2<T>(ListOps::s_safe_get<List<T>>(ll, 0n), ListOps::s_safe_get<List<T>>(ll, 1n));
                return ListOps::s_concat2<T>(fl, ListOps::s_safe_get<List<T>>(ll, 2n));
            }
            else {
                //TODO: we need to add a s_flatten constructor that is either an unroll for model generation or 
                //      an \forall k \exists i, j s.t. get(k) = ll.get(i).get(j) /\ \forall i, j => \exists k s.t. get(k) = ll.get(i).get(j) 
                check count == 4; 

                let fl = ListOps::s_concat2(ListOps::s_safe_get<List<T>>(ll, 0n), ListOps::s_safe_get<List<T>>(ll, 1n));
                let sl = ListOps::s_concat2(ListOps::s_safe_get<List<T>>(ll, 2n), ListOps::s_safe_get<List<T>>(ll, 3n));
                return ListOps::s_concat2(fl, sl);
            }
        }
    }

    method size(): Nat {
        return ListOps::s_size<T>(this);
    }

    method empty(): Bool {
        return ListFlatOps::s_empty<T>(this);
    }

    method front(): T {
        check !ListFlatOps::s_empty<T>(this);
        return ListOps::s_safe_get<T>(this, 0n);
    }

    method frontOrNone(): T? {
        if(ListFlatOps::s_empty<T>(this)) {
            return none;
        }
        else {
            return ListOps::s_safe_get<T>(this, 0n);
        }
    }

    method frontOption(): Option<T> {
        if(ListFlatOps::s_empty<T>(this)) {
            return nothing;
        }
        else {
            return something(ListOps::s_safe_get<T>(this, 0n));
        }
    }

    method frontTry(out? res: T): Bool {
        if(!ListFlatOps::s_empty<T>(this)) {
            res = ListOps::s_safe_get<T>(this, 0n);
            return true;
        }
        else {
            return false;
        }
    }

    method back(): T {
        check !ListFlatOps::s_empty<T>(this);
        return ListOps::s_safe_get<T>(this, ListOps::s_size<T>(this) - 1n);
    }

    method backOrNone(): T? {
        if(ListFlatOps::s_empty<T>(this)) {
            return none;
        }
        else {
            return ListOps::s_safe_get<T>(this, ListOps::s_size<T>(this) - 1n);
        }
    }

    method backOption(): Option<T> {
        if(ListFlatOps::s_empty<T>(this)){
            return nothing;
        }
        else {
            return something(ListOps::s_safe_get<T>(this, ListOps::s_size<T>(this) - 1n));
        }
    }

    method backTry(out? res: T): Bool {
        if(!ListFlatOps::s_empty<T>(this)) {
            res = ListOps::s_safe_get<T>(this, ListOps::s_size<T>(this) - 1n);
            return true;
        }
        else {
            return false;
        }
    }

    method get(i: Nat): T {
        check i < ListOps::s_size<T>(this);
        return ListOps::s_safe_get<T>(this, i);
    }

    method getOrNone(i: Nat): T? {
        if(i < ListOps::s_size<T>(this)) {
            return ListOps::s_safe_get<T>(this, i);
        }
        else {
            return none;
        }
    }

    method getOption(i: Nat): Option<T> {
        if(i < ListOps::s_size<T>(this)) {
            return something(ListOps::s_safe_get<T>(this, i));
        }
        else {
            return nothing;
        }
    }

    method getTry(i: Nat, out? res: T): Bool {
        if(i < ListOps::s_size<T>(this)) {
            res = ListOps::s_safe_get<T>(this, i);
            return true;
        }
        else {
            return false;
        }
    }

    recursive? method single(p: recursive? pred(_: T) -> Bool): T {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);
        check idx !== none;

        let lastidx = ListOps::s_find_last_pred<T>[recursive?](this, p);
        check lastidx === idx;

        return ListOps::s_safe_get<T>(this, idx);
    }

    recursive? method singleOrNone(p: recursive? pred(_: T) -> Bool): T? {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);
        if(idx === none) {
            return none;
        }
        else {
            let lastidx = ListOps::s_find_last_pred<T>[recursive?](this, p);

            if(idx !== lastidx) {
                return none;
            }
            else {
                return ListOps::s_safe_get<T>(this, idx);
            }
        }
    }

    recursive? method singleOption(p: recursive? pred(_: T) -> Bool): Option<T> {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);
         if(idx === none) {
            return nothing;
        }
        else {
            let lastidx = ListOps::s_find_last_pred<T>[recursive?](this, p);

            if(idx !== lastidx) {
                return nothing;
            }
            else {
                return something(ListOps::s_safe_get<T>(this, idx));
            }
        }
    }

    recursive? method singleTry(out? res: T, p: recursive? pred(_: T) -> Bool): Bool {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);
        if(idx === none) {
            return false;
        }
        else {
            let lastidx = ListOps::s_find_last_pred<T>[recursive?](this, p);

            if(idx !== lastidx) {
                return false;
            }
            else {
                res = ListOps::s_safe_get<T>(this, idx);
                return true;
            }
        }
    }

    recursive? method allOf(p: recursive? pred(_: T) -> Bool): Bool {
        return !ListOps::s_has_pred<T>[recursive?](this, recursive? pred(v: T): Bool => !p[recursive?](v));
    }

    recursive? method allOf(p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        return !ListOps::s_has_pred_idx<T>[recursive?](this, recursive? pred(v: T, i: Nat): Bool => !p[recursive?](v, i));
    }

    recursive? method noneOf(p: recursive? pred(_: T) -> Bool): Bool {
        return !ListOps::s_has_pred<T>[recursive?](this, p);
    }

    recursive? method noneOf(p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        return !ListOps::s_has_pred_idx<T>[recursive?](this, p);
    }

    recursive? method someOf(p: recursive? pred(_: T) -> Bool): Bool {
        return ListOps::s_has_pred<T>[recursive?](this, p);
    }

    recursive? method someOf(p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        return ListOps::s_has_pred_idx<T>[recursive?](this, p);
    }

    recursive? method countIf(p: recursive? pred(_: T) -> Bool): Nat {
        return ListOps::s_size<T>(ListOps::s_filter<T>[recursive?](this, p));
    }

    recursive? method countIf(p: recursive? pred(_: T, _: Nat) -> Bool): Nat {
        return ListOps::s_size<T>(ListOps::s_filter_idx<T>[recursive?](this, p));
    }

    method {when T grounded KeyType} contains(v: T): Bool {
        return ListOps::s_has_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));
    }

    method containsNone(): Bool {
        return ListOps::s_has_pred<T>(this, pred(e: T): Bool => e === none);
    }

    method containsNothing(): Bool {
        return ListOps::s_has_pred<T>(this, pred(e: T): Bool => e === nothing);
    }

    method {when T grounded KeyType} count(v: T): Int {
        return ListOps::s_size<T>(ListOps::s_filter<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e)));
    }

    method countNone(): Bool {
        return ListOps::s_size<T>(ListOps::s_filter<T>(this, pred(e: T): Bool => e === none));
    }

    method countNothing(): Bool {
        return ListOps::s_size<T>(ListOps::s_filter<T>(this, pred(e: T): Bool => e === nothing));
    }

    method {when T unique Orderable} min(): T {
        check !ListFlatOps::s_empty<T>(this);

        let midx = ListOps::s_min_arg<T, T>(this, fn(v: T): T => v);
        return ListOps::s_safe_get<T>(this, midx);
    }

    method {when T unique Orderable} max(): T {
        check !ListFlatOps::s_empty<T>(this);

        let midx = ListOps::s_max_arg<T, T>(this, fn(v: T): T => v);
        return ListOps::s_safe_get<T>(this, midx);
    }

    method {when T unique Orderable} minOrNone(): T? {
        if(ListFlatOps::s_empty<T>(this)) {
            return none;
        }
        else {
            let midx = ListOps::s_min_arg<T, T>(this, fn(v: T): T => v);
            return ListOps::s_safe_get<T>(this, midx);
        }
    }

    method {when T unique Orderable} maxOrNone(): T? {
        if(ListFlatOps::s_empty<T>(this)) {
            return none;
        }
        else {
            let midx = ListOps::s_max_arg<T, T>(this, fn(v: T): T => v);
            return ListOps::s_safe_get<T>(this, midx);
        }
    }

    method {when T unique Orderable} minOption(): Option<T> {
        if(ListFlatOps::s_empty<T>(this)) {
            return nothing;
        }
        else {
            let midx = ListOps::s_min_arg<T, T>(this, fn(v: T): T => v);
            return something(ListOps::s_safe_get<T>(this, midx));
        }
    }

    method {when T unique Orderable} maxOrOption(): Option<T> {
        if(ListFlatOps::s_empty<T>(this)) {
            return nothing;
        }
        else {
            let midx = ListOps::s_max_arg<T, T>(this, fn(v: T): T => v);
            return something(ListOps::s_safe_get<T>(this, midx));
        }
    }

    method {when T unique Orderable} minTry(out? res: T): Bool {
        if(ListFlatOps::s_empty<T>(this)) {
            return false;
        }
        else {
            let midx = ListOps::s_min_arg<T, T>(this, fn(v: T): T => v);
            res = ListOps::s_safe_get<T>(this, midx);
            return true;
        }
    }

    method {when T unique Orderable} maxTry(out? res: T): Bool {
        if(ListFlatOps::s_empty<T>(this)) {
            return false;
        }
        else {
            let midx = ListOps::s_max_arg<T, T>(this, fn(v: T): T => v);
            res = ListOps::s_safe_get<T>(this, midx);
            return true;
        }
    }

    method argMin<U unique Orderable=?>(f: recursive? fn(_: T) -> U): T {
        check !ListFlatOps::s_empty<T>(this);

        let midx = ListOps::s_min_arg_idx<T, U>[recursive?](this, f);
        return ListOps::s_safe_get<T>(this, midx);
    }

    method argMax<U unique Orderable=?>(f: recursive? fn(_: T) -> U): T {
        check !ListFlatOps::s_empty<T>(this);

        let midx = ListOps::s_max_arg_idx<T, U>[recursive?](this, f);
        return ListOps::s_safe_get<T>(this, midx);
    }

    method argMinOrNone<U unique Orderable=?>(f: recursive? fn(_: T) -> U): T? {
        if(ListFlatOps::s_empty<T>(this)) {
            return none;
        }
        else {
            let midx = ListOps::s_min_arg_idx<T, U>[recursive?](this, f);
            return ListOps::s_safe_get<T>(this, midx);
        }
    }

    method argMaxOrNone<U unique Orderable=?>(f: recursive? fn(_: T) -> U): T? {
        if(ListFlatOps::s_empty<T>(this)) {
            return none;
        }
        else {
            let midx = ListOps::s_max_arg_idx<T, U>[recursive?](this, f);
            return ListOps::s_safe_get<T>(this, midx);
        }
    }

    method argMinOption<U unique Orderable=?>(f: recursive? fn(_: T) -> U): Option<T> {
        if(ListFlatOps::s_empty<T>(this)) {
            return nothing;
        }
        else {
            let midx = ListOps::s_min_arg_idx<T, U>[recursive?](this, f);
            return something(ListOps::s_safe_get<T>(this, midx));
        }
    }

    method argMaxOrOption<U unique Orderable=?>(f: recursive? fn(_: T) -> U): Option<T> {
        if(ListFlatOps::s_empty<T>(this)) {
            return nothing;
        }
        else {
            let midx = ListOps::s_max_arg_idx<T, U>[recursive?](this, f);
            return something(ListOps::s_safe_get<T>(this, midx));
        }
    }

    method argMinTry<U unique Orderable=?>(out? res: T, f: recursive? fn(_: T) -> U): Bool {
        if(ListFlatOps::s_empty<T>(this)) {
            return false;
        }
        else {
            let midx = ListOps::s_min_arg_idx<T, U>[recursive?](this, f);
            res = ListOps::s_safe_get<T>(this, midx);
            return true;
        }
    }

    method argMaxTry<U unique Orderable=?>(out? res: T, f: recursive? fn(_: T) -> U): Bool {
        if(ListFlatOps::s_empty<T>(this)) {
            return false;
        }
        else {
            let midx = ListOps::s_max_arg_idx<T, U>[recursive?](this, f);
            res = ListOps::s_safe_get<T>(this, midx);
            return true;
        }
    }

    method {when T unique Algebraic} sum(): T {
        return ListOps::s_sum<T>(this);
    }

    recursive? method find(p: recursive? pred(_: T) -> Bool): T {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        check idx !== none;
        return ListOps::s_safe_get<T>(this, idx);
    }

    recursive? method find(p: recursive? pred(_: T, _: Nat) -> Bool): T {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        check idx !== none;
        return ListOps::s_safe_get<T>(this, idx);
    }

    recursive? method findOrNone(p: recursive? pred(_: T) -> Bool): T? {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        if(idx === none) {
            return none;
        }
        else {
            return ListOps::s_safe_get<T>(this, idx);
        }
    }

    recursive? method findOrNone(p: recursive? pred(_: T, _: Nat) -> Bool): T? {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return none;
        }
        else {
            return ListOps::s_safe_get<T>(this, idx);
        }
    }

    recursive? method findOption(p: recursive? pred(_: T) -> Bool): Option<T> {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(ListOps::s_safe_get<T>(this, idx));
        }
    }

    recursive? method findOption(p: recursive? pred(_: T, _: Nat) -> Bool): Option<T> {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(ListOps::s_safe_get<T>(this, idx));
        }
    }

    recursive? method findTry(out? res: T, p: recursive? pred(_: T) -> Bool): Bool {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = ListOps::s_safe_get<T>(this, idx);
            return true;
        }
    }

    recursive? method findTry(out? res: T, p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = ListOps::s_safe_get<T>(this, idx);
            return true;
        }
    }

    recursive? method findLast(p: recursive? pred(_: T) -> Bool): T {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        check idx !== none;
        return ListOps::s_safe_get<T>(this, idx);
    }

    recursive? method findLast(p: recursive? pred(_: T, _: Nat) -> Bool): T {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        check idx !== none;
        return ListOps::s_safe_get<T>(this, idx);
    }

    recursive? method findLastOrNone(p: recursive? pred(_: T) -> Bool): T? {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        if(idx === none) {
            return none;
        }
        else {
            return ListOps::s_safe_get<T>(this, idx);
        }
    }

    recursive? method findLastOrNone(p: recursive? pred(_: T, _: Nat) -> Bool): T? {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return none;
        }
        else {
            return ListOps::s_safe_get<T>(this, idx);
        }
    }

    recursive? method findLastOption(p: recursive? pred(_: T) -> Bool): Option<T> {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(ListOps::s_safe_get<T>(this, idx));
        }
    }

    recursive? method findLastOption(p: recursive? pred(_: T, _: Nat) -> Bool): Option<T> {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(ListOps::s_safe_get<T>(this, idx));
        }
    }

    recursive? method findLastTry(out? res: T, p: recursive? pred(_: T) -> Bool): Bool {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = ListOps::s_safe_get<T>(this, idx);
            return true;
        }
    }

    recursive? method findLastTry(out? res: T, p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = ListOps::s_safe_get<T>(this, idx);
            return true;
        }
    }

    recursive? method findIndexOf(p: recursive? pred(_: T) -> Bool): Nat {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        check idx !== none;
        return idx;
    }

    recursive? method findIndexOf(p: recursive? pred(_: T, _: Nat) -> Bool): Nat {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        check idx !== none;
        return idx;
    }

    recursive? method findIndexOfOrNone(p: recursive? pred(_: T) -> Bool): Nat? {
        return ListOps::s_find_pred<T>[recursive?](this, p);
    }

    recursive? method findIndexOfOrNone(p: recursive? pred(_: T, _: Nat) -> Bool): Nat? {
        return ListOps::s_find_pred_idx<T>[recursive?](this, p);
    }

    recursive? method findIndexOfOption(p: recursive? pred(_: T) -> Bool): Option<Nat> {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(idx);
        }
    }

    recursive? method findIndexOfOption(p: recursive? pred(_: T, _: Nat) -> Bool): Option<Nat> {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(idx);
        }
    }

    recursive? method findIndexOfTry(out? res: Nat, p: recursive? pred(_: T) -> Bool): Bool {
        let idx = ListOps::s_find_pred<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = idx;
            return true;
        }
    }

    recursive? method findIndexOfTry(out? res: Nat, p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        let idx = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = idx;
            return true;
        }
    }

    recursive? method findIndexOfLast(p: recursive? pred(_: T) -> Bool): Nat {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        check idx !== none;
        return idx;
    }

    recursive? method findIndexOfLast(p: recursive? pred(_: T, _: Nat) -> Bool): Nat {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        check idx !== none;
        return idx;
    }

    recursive? method findIndexOfLastOrNone(p: recursive? pred(_: T) -> Bool): Nat? {
        return ListOps::s_find_last_pred<T>[recursive?](this, p);
    }

    recursive? method findIndexOfLastOrNone(p: recursive? pred(_: T, _: Nat) -> Bool): Nat? {
        return ListOps::s_find_last_pred_idx<T>[recursive?](this, p);
    }

    recursive? method findIndexOfLastOption(p: recursive? pred(_: T) -> Bool): Option<Nat> {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(idx);
        }
    }

    recursive? method findIndexOfLastOption(p: recursive? pred(_: T, _: Nat) -> Bool): Option<Nat> {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return nothing;
        }
        else {
            return something(idx);
        }
    }

    recursive? method findIndexOfLastTry(out? res: Nat, p: recursive? pred(_: T) -> Bool): Bool {
        let idx = ListOps::s_find_last_pred<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = idx;
            return true;
        }
    }

    recursive? method findIndexOfLastTry(out? res: Nat, p: recursive? pred(_: T, _: Nat) -> Bool): Bool {
        let idx = ListOps::s_find_last_pred_idx<T>[recursive?](this, p);

        if(idx === none) {
            return false;
        }
        else {
            res = idx;
            return true;
        }
    }

    method {when T grounded KeyType} indexOf(v: T): Nat {
        let idx = ListOps::s_find_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));

        check idx !== none;
        return idx;
    }

    method {when T grounded KeyType} indexOfOrNone(v: T): Nat? {
        return ListOps::s_find_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));
    }

    method {when T grounded KeyType} indexOfOption(v: T): Option<Nat> {
        let idx = ListOps::s_find_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));

        if(idx === none) {
            return nothing;
        }
        else {
            return something(idx);
        }
    }

    method {when T grounded KeyType} indexOfTry(out? res: Nat, v: T): Bool {
        let idx = ListOps::s_find_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));

        if(idx === none) {
            return false;
        }
        else {
            res = idx;
            return true;
        }
    }

    method {when T grounded KeyType} indexOfLast(v: T): Nat {
        let idx = ListOps::s_find_last_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));

        check idx !== none;
        return idx;
    }

    method {when T grounded KeyType} indexOfLastOrNone(v: T): Nat? {
        return ListOps::s_find_last_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));
    }

    method {when T grounded KeyType} indexOfLastOption(v: T): Option<Nat> {
        let idx = ListOps::s_find_last_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));

        if(idx === none) {
            return nothing;
        }
        else {
            return something(idx);
        }
    }

    method {when T grounded KeyType} indexOfLastTry(out? res: Nat, v: T): Bool {
        let idx = ListOps::s_find_last_pred<T>(this, pred(e: T): Bool => KeyType::equal<T>(v, e));

        if(idx === none) {
            return false;
        }
        else {
            res = idx;
            return true;
        }
    }

    recursive? method filter(p: recursive? pred(_: T) -> Bool): List<T> {
        return ListOps::s_filter<T>[recursive?](this, p);
    }

    recursive? method filter(p: recursive? pred(_: T, _: Nat) -> Bool): List<T> {
        return ListOps::s_filter_idx<T>[recursive?](this, p);
    }

    method filterType<U>(): List<U> {
        let fl = ListOps::s_filter<T>(this, pred(v: T): Bool => v istype U);
        let tl = ListOps::s_map<T, U>(fl, fn(v: T): U => ListOps::s_safeAs<T, U>(v));
        return tl;
    }

    method filterSome(): List<T!Some> {
        let fl = ListOps::s_filter<T>(this, pred(v: T): Bool => v istype T!Some);
        let tl = ListOps::s_map<T, T!Some>(fl, fn(v: T): T!Some => ListOps::s_safeAs<T, T!Some>(v));
        return tl;
    }

    method {when T unique IOption} filterSomething(): List<T!IOptionT> {
        let fl = ListOps::s_filter<T>(this, pred(v: T): Bool => v istype Something<T!IOptionT>);
        let tl = ListOps::s_map<T, T!IOptionT>(fl, fn(v: T): T!IOptionT => ListOps::s_safeAs<T, Something<T!IOptionT>>(v).value());
        return tl;

        let somes = ListOps::s_filter_type<T, Option<T!IOptionT>>(this);
        return ListOps::s_map<Option<T!IOptionT>, T!IOptionT>(somes, fn(v: Option<T!IOptionT>): T!IOptionT => v.value());
    }

    method castType<U>(): List<U> {
        return ListOps::s_map<T, U>(this, fn(v: T): U => v astype U);
    }

    method slice(start?: Nat=0n, end?: Nat=ListOperators::s_size<T>(this)): List<T> {
        check start <= end;
        check end <= ListOps::s_size<T>(this);
        return ListOps::s_slice<T>(this, start, end);
    }

    method removeFirst(k: Nat): List<T> {
        check k <= ListOps::s_size<T>(this);
        return ListOps::s_slice<T>(this, k, ListOps::s_size<T>(this));
    } 

    method removeLast(k: Nat): List<T> {
        check k <= ListOps::s_size<T>(this);
        return ListOps::s_slice<T>(this, 0, ListOps::s_size<T>(this) - k);
    }

    recursive? method takeWhile(p: recursive? pred(_: T) -> Bool): List<T>  {
        let lpos = ListOps::s_find_pred<T>[recursive?](this, recursive? pred(v: T) => !p[recursive?](v));

        if(lpos === none) {
            return this;
        }
        else {
            return ListOps::s_slice<T>(this, 0, lpos);
        }
    }

    recursive? method takeWhile(p: recursive? pred(_: T, _: Nat) -> Bool): List<T>  {
        let lpos = ListOps::s_find_pred_idx<T>[recursive?](this, recursive? pred(v: T, i: Nat) => !p[recursive?](v, i));

        if(lpos === none) {
            return this;
        }
        else {
            return ListOps::s_slice<T>(this, 0, lpos);
        }
    }

    recursive? method discardWhile(p: recursive? pred(_: T) -> Bool): List<T> {
        let lpos = ListOps::s_find_pred<T>[recursive?](this, recursive? pred(v: T) => !p[recursive?](v));

        if(lpos === none) {
            return List<T>@{};
        }
        else {
            return ListOps::s_slice<T>(this, lpos, ListOps::s_size<T>(this));
        }
    }

    recursive? method discardWhile(p: recursive? pred(_: T, _: Nat) -> Bool): List<T> {
        let lpos = ListOps::s_find_pred_idx<T>[recursive?](this, recursive? pred(v: T, i: Nat) => !p[recursive?](v, i));

        if(lpos === none) {
            return List<T>@{};
        }
        else {
            return ListOps::s_slice<T>(this, lpos, ListOps::s_size<T>(this));
        }
    }

    recursive? method takeUntil(p: recursive? pred(_: T) -> Bool): List<T> {
        let lpos = ListOps::s_find_pred<T>[recursive?](this, p);

        if(lpos === none) {
            return this;
        }
        else {
            return ListOps::s_slice<T>(this, 0, lpos);
        }
    }

    recursive? method takeUntil(p: recursive? pred(_: T, _: Nat) -> Bool): List<T> {
        let lpos = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(lpos === none) {
            return this;
        }
        else {
            return ListOps::s_slice<T>(this, 0, lpos);
        }
    }

    recursive? method discardUntil(p: recursive? pred(_: T) -> Bool): List<T>  {
        let lpos = ListOps::s_find_pred<T>[recursive?](this, p);

        if(lpos === none) {
            return this;
        }
        else {
            return ListOps::s_slice<T>(this, lpos, ListOps::s_size<T>(this));
        }
    }

    recursive? method discardUntil(p: recursive? pred(_: T, _: Nat) -> Bool): List<T>  {
        let lpos = ListOps::s_find_pred_idx<T>[recursive?](this, p);

        if(lpos === none) {
            return this;
        }
        else {
            return ListOps::s_slice<T>(this, lpos, ListOps::s_size<T>(this));
        }
    }

    recursive? method map<U=?>(f: recursive? fn(_: T) -> U): List<U>  {
        return ListOps::s_map<T, U>[recursive?](this, f);
    }

    recursive? method map<U=?>(f: recursive? fn(_: T, _: Nat) -> U): List<U>  {
        return ListOps::s_map_idx<T, U>[recursive?](this, f);
    }

    recursive? method mapNone<U=?>(f: recursive? fn(_: T!Some) -> U): List<U>  {
        return ListOps::s_map<T, U>[recursive?](this, recursive? fn(v: T): U => {
            if(v === none) {
                return none;
            }
            else {
                return f[recursive?](v);
            }
        });
    }

    recursive? method mapNone<U=?>(f: recursive? fn(_: T, _: Nat) -> U): List<U>  {
        return ListOps::s_map_idx<T, U>[recursive?](this, recursive? fn(v: T, i: Nat): U => {
            if(v === none) {
                return none;
            }
            else {
                return f[recursive?](v, i);
            }
        });
    }

    recursive? method {when T unique IOption} mapOption<U=?>(f: recursive? fn(_: T!IOptionT) -> U): List<U>  {
        return ListOps::s_map<T, U>[recursive?](this, recursive? fn(v: T): U => {
            if(v === nothing) {
                return nothing;
            }
            else {
                return something(f[recursive?](v.value()));
            }
        });
    }

    recursive? method {when T unique IOption} mapOption<U=?>(f: recursive? fn(_: T!IOptionT, _: Nat) -> U): List<U>  {
        return ListOps::s_map_idx<T, U>[recursive?](this, recursive? fn(v: T, i: Nat): U => {
            if(v === nothing) {
                return nothing;
            }
            else {
                return something(f[recursive?](v.value(), i));
            }
        });
    }

    method {when T grounded KeyType} project<U=?>(m: Map<T, U>): List<U> {
        return ListOps::s_map<T, U>(this, fn(v: T): U => {
            return m.get(v);
        });
    }

    method {when T grounded KeyType} projectOrNone<U=?>(m: Map<T, U>): List<U?> {
        return ListOps::s_map_Helper<T, U?>(this, fn(v: T): U? => {
            return m.getOrNone(v);
        });
    }

    method {when T grounded KeyType} projectOption<U=?>(m: Map<T, U>): List<Option<U>> {
        return ListOps::s_map_Helper<T, Option<U>>(this, fn(v: T): Option<U> => {
            return m.getOption(v);
        });
    }

    method {when T grounded KeyType} projectImage<U=?>(m: Map<T, U>): List<U> {
        let img = ListOps::s_filter<T>(pred(v: T) => m.has(v));
        return ListOps::s_map<T, U>(img, fn(v: T): U => m.get(v));
    }

    recursive? method join<U=?>(ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> {
        return ListOps::s_join<T, U>[recursive?](this, ol, p);
    }

    recursive? method joinGroup<U=?>(ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, List<U>]> {
        return ListOps::s_map<T, [T, List<U>]>[recursive?](this, recursive? fn(v: T): [T, List<U>] => {
            let ul = ListOps::s_filter<U>[recursive?](ol, pred(u: U) => p[recursive?](v, u));
            return [v, ul];
        });
    }

    method append(l: List<T>): List<T> {
        return ListOps::s_concat2<T>(this, l);
    }

    method prepend(l: List<T>): List<T> {
        return ListOps::s_concat2<T>(l, this);
    }

    recursive? method partition<K grounded KeyType=?>(pf: recursive? fn(_: T) -> K): Map<K, List<T>> {
        let allkeys = ListOps::s_map<T, K>[recursive?](this, pf);
        let ukeys = ListOps::s_unique<K>(allkeys);

        let kopts = ListOps::s_zip<K, T>(allkeys, this);
        let classes = ListOps::s_map<K, [K, List<T>]>(ukeys, fn(k: K): [K, List<T>] => {
            let pl = ListOps::s_filter<[K, T]>(kopts, pred(v: [K, T]) => k === v.0);
            let tl = ListOps::s_map<[K, T], T>(pl, fn(vv: [K, T]): T => vv.1);

            return [k, tl];
        });

        return Map<K, T>@{...classes};
    }

    recursive? method sort(cmp: recursive? pred(_: T, _: T) -> Bool): List<T> {
        return ListOps::s_sort<T>[recursive?](this, cmp);
    }

    recursive? method uniqueify(eq: recursive? pred(_: T, _: T) -> Bool): List<T> {
       return ListOps::s_unique<T>[recursive?](this, eq);
    }

    recursive? method unique(eq: recursive? pred(_: T, _: T) -> Bool): Bool {
       return ListOps::s_isunique<T>[recursive?](this, eq);
    }

    method reverse(): List<T> {
        return ListOps::s_reverse<T>(this);
    }

    method pushBack(v: T): List<T> {
        return ListOps::s_concat2<T>(this, List<T>@{v});
    }

    method pushFront(v: T): List<T> {
        return ListOps::s_concat2<T>(List<T>@{v}, this);
    }

    method popBack(): List<T> {
        check !ListFlatOps::s_empty<T>(this);

        return ListOps::s_slice<T>(this, 0, ListOps::s_size<T>(this) - 1);
    }

    method popFront(): List<T> {
        check !ListFlatOps::s_empty<T>(this);

        return ListOps::s_slice<T>(this, 1, ListOps::s_size<T>(this));
    }

    method set(i: Nat, v: T): List<T> {
        check i < ListOps::s_size<T>(this);

        let count = ListOps::s_size<T>(this);
        let vl = List<T>@{v};

        if(count == 1) {
            return vl;
        }
        else {
            if(i == 0) {
                return ListOps::s_concat2<T>(vl, ListOps::s_slice<T>(this, 1, count));
            }
            elif(i == count - 1) {
                return ListOps::s_concat2<T>(ListOps::s_slice<T>(this, 0, count - 1), vl);
            }
            else {
                let front = ListOps::s_slice<T>(this, 0, i);
                let back = ListOps::s_slice<T>(this, i + 1, count);

                return ListOps::s_concat2<T>(ListOps::s_concat2<T>(front, vl), back);
            }
        } 
    }

    method remove(i: Nat): List<T> {
        check i < ListOps::s_size<T>(this);

        let count = ListOps::s_size<T>(this);

        if(count == 1) {
            return List<T>@{};
        }
        else {
            if(i == 0) {
                return ListOps::s_slice<T>(this, 1, count);
            }
            elif(i == count - 1) {
                return ListOps::s_slice<T>(this, 0, count - 1);
            }
            else {
                let front = ListOps::s_slice<T>(this, 0, i);
                let back = ListOps::s_slice<T>(this, i + 1, count);

                return ListOps::s_concat2<T>(front, back);
            }
        } 
    }

    //
    //TODO: ref versions of the add/remove/set operators
    //

    //
    //TODO: Convert to Map<K, V> and Set<T> operations
    //
}
