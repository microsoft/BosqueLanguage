//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

entity List<T> provides Object, Expandoable<T>, PODType when T PODType, APIType when T APIType {
    hidden static s_size(l: List<T>): Int # list_size
    hidden static s_unsafe_get(l: List<T>, i: Int): T # list_unsafe_get

    hidden static s_unsafe_push(l: List<T>, v: T): T # list_unsafe_push
    hidden static s_unsafe_set(l: List<T>, idx: Int, v: T): T # list_unsafe_set
    
    hidden recursive static {when T KeyType} s_toset(l: List<T>, idx: Int, r: Set<T>): Set<T> {
        if(idx == List<T>::s_size(l)) {
            return rs;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_toset(l, idx + 1, Set<T>::s_add(r, val));
        }
    }

    hidden recursive static s_all(l: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): Bool {
        if(idx == List<T>::s_size(l)) {
            return true;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(!p[recursive?](val)) {
                return false;
            }
            else {
                return List<T>::s_all[recursive](l, idx + 1, p);
            }
        }
    }

    hidden recursive static s_none(l: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): Bool {
        if(idx == List<T>::s_size(l)) {
            return true;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return false;
            }
            else {
                return List<T>::s_none[recursive](l, idx + 1, p);
            }
        }
    }

    hidden recursive static s_any(l: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): Bool {
        if(idx == List<T>::s_size(l)) {
            return false;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return true;
            }
            else {
                return List<T>::s_any[recursive](l, idx + 1, p);
            }
        }
    }

    hidden recursive static s_indexof(l: List<T>, idx: Int, end: Int, p: recursive? fn(_: T) -> Bool): Int {
        if(idx == end) {
            return idx;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return idx;
            }
            else {
                return List<T>::s_indexof[recursive](l, idx + 1, end, p);
            }
        }
    }

    hidden recursive static s_indexoflast(l: List<T>, start: Int, idx: Int, p: recursive? fn(_: T) -> Bool): Int {
        if(idx == start) {
            return idx;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return idx;
            }
            else {
                return List<T>::s_indexoflast[recursive](l, start, idx - 1, p);
            }
        }
    }

    hidden recursive static s_indexofnot(l: List<T>, idx: Int, end: Int, p: recursive? fn(_: T) -> Bool): Int {
        if(idx == end) {
            return idx;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(!p[recursive?](val)) {
                return idx;
            }
            else {
                return List<T>::s_indexofnot[recursive](l, idx + 1, end, p);
            }
        }
    }

    hidden recursive static s_indexoflastnot(l: List<T>, start: Int, idx: Int, p: recursive? fn(_: T) -> Bool): Int {
        if(idx == start) {
            return idx;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(!p[recursive?](val)) {
                return idx;
            }
            else {
                return List<T>::s_indexoflastnot[recursive](l, start, idx - 1, p);
            }
        }
    }

    hidden recursive static s_count(l: List<T>, idx: Int, acc: Int, p: recursive? fn(_: T) -> Bool): Int {
        if(idx == List<T>::s_size(l)) {
            return acc;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let nacc = p[recursive?](val) ? acc + 1 : acc;
            
            return List<T>::s_count[recursive](l, idx + 1, nacc, p);
        }
    }

    hidden recursive static s_countnot(l: List<T>, idx: Int, acc: Int, p: recursive? fn(_: T) -> Bool): Int {
        if(idx == List<T>::s_size(l)) {
            return acc;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let nacc = !p[recursive?](val) ? acc + 1 : acc;
            
            return List<T>::s_count[recursive](l, idx + 1, nacc, p);
        }
    }

    hidden recursive static {when T KeyType} s_indexof_keytype(l: List<T>, idx: Int, end: Int, cv: T): Int {
        if(idx == end) {
            return idx;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(KeyType::equal(cv, val)) {
                return idx;
            }
            else {
                return List<T>::s_indexof_keytype[recursive](l, idx + 1, end, cv);
            }
        }
    }

    hidden recursive static {when T KeyType} s_indexoflast_keytype(l: List<T>, start: Int, idx: Int, cv: T): Int {
        if(idx == start) {
            return idx;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(KeyType::equal(cv, val)) {
                return idx;
            }
            else {
                return List<T>::s_indexoflast_keytype[recursive](l, start, idx - 1, cv);
            }
        }
    }

    hidden recursive static {when T KeyType} s_count_keytype(l: List<T>, idx: Int, acc: Int, cv: T): Int {
        if(idx == List<T>::s_size(l)) {
            return acc;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let nacc = (KeyType::equal(cv, val)) ? acc + 1 : acc;
            
            return List<T>::s_count_keytype[recursive](l, idx + 1, nacc, cv);
        }
    }

    hidden recursive static {when T KeyType} s_min(l: List<T>, idx: Int, acc: T): T {
        if(idx == List<T>::s_size(l)) {
            return acc;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_min[recursive](l, idx + 1, acc < val ? acc : val);
        }
    }

    hidden recursive static {when T KeyType} s_max(l: List<T>, idx: Int, acc: T): T {
        if(idx == List<T>::s_size(l)) {
            return List<T>::s_size(l);
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_max[recursive](l, idx + 1, acc > val ? acc : val);
        }
    }

    hidden recursive static {when T Int | BigInt} s_sum(l: List<T>, idx: Int, acc: T): T {
        if(idx == List<T>::s_size(l)) {
            return acc;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_sum[recursive](l, idx + 1, acc + val);
        }
    }


    hidden recursive static s_filter(l: List<T>, lp: List<U>, idx: Int, p: recursive? fn(_: T) -> Bool): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return List<T>::s_filter(l, List<T>::s_unsafe_push(lp, val), idx + 1, p);
            }
            else {
                return List<T>::s_filter(l, lp, idx + 1, p);
            }
        }
    }

    hidden recursive static s_filternot(l: List<T>, lp: List<U>, idx: Int, p: recursive? fn(_: T) -> Bool): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(!p[recursive?](val)) {
                return List<T>::s_filternot(l, List<T>::s_unsafe_push(lp, val), idx + 1, p);
            }
            else {
                return List<T>::s_filternot(l, lp, idx + 1, p);
            }
        }
    }

    hidden recursive static s_ofttype<U>(l: List<T>, lp: List<U>, idx: Int): List<U> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(val->is<U>()) {
                return List<T>::s_ofttype<U>(l, List<U>::s_unsafe_push(lp, val->as<U>()), idx + 1);
            }
            else {
                return List<T>::s_ofttype<U>(l, lp, idx + 1);
            }
        }
    }

    hidden recursive static s_cast<U>(l: List<T>, lp: List<U>, idx: Int): List<U> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_cast<U>(l, List<U>::s_unsafe_push(lp, val->as<U>()), idx + 1);
        }
    }

    hidden recursive static s_takek(l: List<T>, lp: List<T>, idx: Int, k: Int): List<T> {
        if(k == 0) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_takek<U>(l, List<T>::s_unsafe_push(lp, val), idx + 1, k - 1);
        }
    }

    hidden recursive static s_slice(l: List<T>, start: Int, end: Int): List<T> {
        return List<T>::s_takek(l, List<T>@{}, start, end - start);
    }

    hidden recursive static s_takewhile(l: List<T>, lp: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(!p[recursive?](val)) {
                return lp;
            }
            else {
                return List<T>::s_takewhile(l, List<T>::s_unsafe_push(lp, val), idx + 1, p);
            }
        }
    }

    hidden recursive static s_discardwhile(l: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): List<T> {
        if(idx == List<T>::s_size(l)) {
            return List<T>@{};
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return List<T>::s_discardwhile(l, idx + 1, p);
            }
            else {
                return List<T>::s_takek(l, List<T>@{}, idx, List<T>::s_size(l) - idx);
            }
        }
    }

    hidden recursive static s_takeuntil(l: List<T>, lp: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(p[recursive?](val)) {
                return lp;
            }
            else {
                return List<T>::s_takeuntil(l, List<T>::s_unsafe_push(lp, val), idx + 1, p);
            }
        }
    }

    hidden recursive static s_discarduntil(l: List<T>, idx: Int, p: recursive? fn(_: T) -> Bool): List<T> {
        if(idx == List<T>::s_size(l)) {
            return List<T>@{};
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            if(!p[recursive?](val)) {
                return List<T>::s_discarduntil(l, idx + 1, p);
            }
            else {
                return List<T>::s_takek(l, List<T>@{}, idx, List<T>::s_size(l) - idx);
            }
        }
    }

    hidden recursive static {when T KeyType} s_unique(l: List<T>, lp: List<T>, idx: Int): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let has = List<T>::s_indexof_keytype[recursive](lp, 0, List<T>::s_size(lp), val) != List<T>::s_size(lp);
            if(has) {
                return List<T>::s_unique(l, lp, idx + 1);
            }
            else {
                return List<T>::s_unique(l, List<T>::s_unsafe_push(lp, val), idx + 1);
            }
        }
    }

    hidden recursive static s_reverse(l: List<T>, lp: List<T>, idx: Int): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_reverse(l, List<T>::s_unsafe_set(lp, (List<T>::s_size(l) - 1) - idx, val), idx + 1);
        }
    }

    hidden recursive static s_map<U>(l: List<T>, lp: List<U>, idx: Int, f: recursive? fn(_: T) -> U): List<U> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_map<U>(l, List<T>::s_unsafe_push(lp, f[recursive?](val)), idx + 1, f);
        }
    }

    hidden recursive static s_mapIndex<U>(l: List<T>, lp: List<U>, idx: Int, f: recursive? fn(_: Int, _: T) -> U): List<U> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_mapIndex<U>(l, List<T>::s_unsafe_push(lp, f[recursive?](idx, val)), idx + 1, f);
        }
    }

    hidden recursive static {when T KeyType} s_projectWMap<U>(l: List<T>, lp: List<U>, idx: Int, m: Map<T, U>): List<U> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_projectWMap<U>(l, List<T>::s_unsafe_push(lp, m->get(val)), idx + 1);
        }
    }

    hidden recursive static {when T KeyType} s_tryProjectWMap<U>(l: List<T>, lp: List<U>, idx: Int, m: Map<T, U>): List<U> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_tryProjectWMap<U>[recursive](l, List<T>::s_unsafe_push(lp, m->tryGet(val)), idx + 1);
        }
    }

    hidden recursive static s_zipIndex(l: List<T>, lp: List<[Int, T]>, idx: Int): List<[Int, T]> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_zipIndex[recursive](l, List<T>::s_unsafe_push(lp, [idx, val]), idx + 1);
        }
    }

    hidden recursive static s_joinSingle<U>(v: T, ol: List<U>, lp: List<[T, U]>, idx: Int, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> {
        if(idx == List<T>::s_size(ol)) {
            return lp;
        }
        else {
            let val = List<U>::s_unsafe_get(ol, idx);
            if(p[recursive?](v, val)) {
                return List<T>::s_joinSingle<U>[recursive](v, List<[T, U]>::s_unsafe_push(lp, [v, val]), idx + 1, p);
            }
            else {
                return List<T>::s_joinSingle<U>[recursive](v, lp, idx + 1, p);
            }
        }
    }

    hidden recursive static s_join<U>(l: List<T>, ol: List<U>, lp: List<[T, U]>, idx: Int, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let jl = List<T>::s_joinSingle<U>[recursive](val, ol, lp, 0, p);

            return List<T>::s_join<U>[recursive](l, ol, lp, idx + 1, p);
        }
    }

    hidden recursive static s_joinGroupSingle<U>(v: T, ol: List<U>, lp: List<[T, List<U>]>, idx: Int, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> {
        if(idx == List<T>::s_size(ol)) {
            return lp;
        }
        else {
            let val = List<U>::s_unsafe_get(ol, idx);
            if(p[recursive?](v, val)) {
                return List<T>::s_joinGroupSingle<U>[recursive](v, List<T>::s_unsafe_push(lp, [v, val]), idx + 1, p);
            }
            else {
                return List<T>::s_joinGroupSingle<U>[recursive](v, lp, idx + 1, p);
            }
        }
    }

    hidden recursive static s_joinGroup<U>(l: List<T>, ol: List<U>, lp: List<[T, List<U>]>, idx: Int, p: recursive? fn(_: T, _: U) -> Bool): List<[T, List<U>]> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let jl = List<T>::s_joinGroupSingle<U>[recursive](val, ol, List<[T, List<U>]>@{}, 0, p);

            return List<T>::s_joinGroup<U>[recursive](l, ol, List<T>::s_unsafe_push(lp, jl), idx + 1, p);
        }
    }

    hidden recursive static s_append(l: List<T>, lp: List<T>, idx: Int): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            return List<T>::s_append[recursive](l, List<T>::s_unsafe_push(lp, val), idx + 1);
        }
    }

    hidden recursive static s_partitionSingle<K where KeyType>(kk: K, ol: List<T>, lp: List<T>, idx: Int, pf: recursive? fn(_: T) -> K): List<T> {
        if(idx == List<T>::s_size(ol)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(ol, idx);
            let key = pf[recursive?](val);
            if(KeyType::equal(kk, key)) {
                return List<T>::s_partitionSingle[recursive](kk, ol, List<T>::s_unsafe_push(lp, val), idx + 1, pf);
            }
            else {
                return List<T>::s_partitionSingle[recursive](kk, ol, lp, idx + 1, pf);
            }
        }
    }

    hidden recursive static s_partition<K where KeyType>(l: List<T>, mp: Map<K, List<T>>, idx: Int, pf: recursive? fn(_: T) -> K): Map<K, List<T>> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let key = pf[recursive?](val);
            if(Map<K, List<T>>::s_has(key)) {
                return List<T>::s_partition(l, mp, idx + 1, pf);
            }
            else {
                let pfg = List<T>::s_partitionSingle<K>(key, l, List<T>@{}, idx, pf);
                return List<T>::s_partition(l, mp->add(val, pfg), idx + 1, pf);
            }
        }
    }

    hidden recursive static s_sort(l: List<T>, ol: List<T>, idx: Int, sf: recursive? fn(_: T, _: T) -> Bool): List<T> {
        abort;
    }

    hidden recursive static s_toMap<K, V>(l: List<T>, mp: Map<K, V>, idx: Int, kf: recursive? fn(_: T) -> K, vf: recursive? fn(_: T) -> V, merge: Bool): Map<K, V> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let k = kf[recursive?](val);
            let v = vf[recursive?](val);

            if(merge) {
                return List<T>::s_toMap(l, map->insert(k, v), idx + 1, kf, vf);
            }
            else {
                return List<T>::s_toMap(l, map->add(k, v), idx + 1, kf, vf);
            }
        }
    }

    hidden recursive static s_toIndexMap<V>(l: List<T>, mp: Map<Int, V>, idx: Int, vf: recursive? fn(_: T) -> V): Map<Int, V> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let val = List<T>::s_unsafe_get(l, idx);
            let v = vf[recursive?](val);

            return List<T>::s_toIndexMap(l, mp->add(idx, v), idx + 1, kf, vf);
        }
    }

    hidden static s_rangeInBoundsFwd(l: List<T>, start?: Int, end?: Int): Bool {
        let s, e = start ?| 0, end ?| List<T>::s_size(l);
        return 0 <= s && s <= e && e <= List<T>::s_size(l); 
    }

    hidden static srangeInBoundsRev(l: List<T>, start?: Int, end?: Int): Bool {
        let s, e = start ?| -1, end ?| List<T>::s_size(l) - 1;
        return -1 <= s && s <= e && e <= List<T>::s_size(l) - 1;
    }

    method size(): Int {
        return List<T>::s_size(this);
    }

    method empty(): Int {
        return List<T>::s_size(this) == 0;
    }

    method front(): T 
        requires release List<T>::s_size(this) != 0;
    {
        return List<T>::s_unsafe_get(this, 0);
    }

    method tryFront(): T? {
        return List<T>::s_size(this) != 0 ? List<T>::s_unsafe_get(this, 0) : none;
    }

    method last(): T 
        requires release List<T>::s_size(this) != 0;
    {
        List<T>::s_unsafe_get(this, List<T>::s_size(this) - 1);
    }

    method tryLast(): T? {
        return List<T>::s_size(this) != 0 ? List<T>::s_unsafe_get(this, List<T>::s_size(this) - 1) : none;
    }

    method get(i: Int): T 
        requires release 0 <= idx && idx < List<T>::s_size(this);
    {
        return List<T>::s_unsafe_get(this, idx);
    }

    method tryGet(i: Int): T? {
        return 0 <= idx && idx < List<T>::s_size(this) ? List<T>::s_unsafe_get(this, idx) : none;
    }

    method {when T KeyType} toSet(): Set<T> {
        return List<T>::s_toset(this, 0, Set<T>@{});
    }

    recursive? method single(p: recursive? fn(_: T) -> Bool): Bool 
        requires release List<T>::s_size(this) != 0;
    {
        let idx = List<T>::s_indexof[recursive](this, 0, List<T>::s_size(this), p);
        let lastidx = List<T>::s_indexoflast[recursive](this, -1, List<T>::s_size(this) - 1, p);

        check lastidx == idx;
        return List<T>::s_unsafe_get(this, idx);
    }

    recursive? method trySingle(p: recursive? fn(_: T) -> Bool): T? {
        let idx = List<T>::s_indexof[recursive](this, 0, List<T>::s_size(this), p);
        let lastidx = List<T>::s_indexoflast[recursive](this, -1, List<T>::s_size(this) - 1, p);

        return idx == lastidx ? List<T>::s_unsafe_get(this, idx) : none;
    }

    method uniform(i: Int, start?: Int, end?: Int): T
        requires release List<T>::s_size(this) != 0;
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        abort;
    }

    recursive? method allof(p: recursive? fn(_: T) -> Bool): Bool {
        return List<T>::s_size(this) == 0 || List<T>::s_all[recursive](this, 0, p);
    }

    recursive? method noneof(p: recursive? fn(_: T) -> Bool): Bool {
        return List<T>::s_size(this) == 0 || List<T>::s_none[recursive](this, 0, p);
    }

    recursive? method anyof(p: recursive? fn(_: T) -> Bool): Bool {
        return List<T>::s_size(this) != 0 && List<T>::s_any[recursive](this, 0, p);
    }

    recursive? method countIf(p: recursive? fn(_: T) -> Bool): Int {
        return List<T>::s_count[recursive](this, 0, 0, p, List<T>::s_size(this));
    }

    recursive? method countIfNot(p: recursive? fn(_: T) -> Bool): Int {
        return List<T>::s_countnot[recursive](this, 0, 0, p, List<T>::s_size(this));
    }

    method {when T KeyType} contains(v: T): Bool {
        return List<T>::s_indexof_keytype[recursive](this, 0, List<T>::s_size(this), v) != List<T>::s_size(this);
    }

    method {when T KeyType} count(v: T): Int {
        return List<T>::s_count_keytype[recursive](this, 0, 0, v, List<T>::s_size(this));
    }

    method {when T KeyType} min(): KeyType 
        requires release List<T>::s_size(this) != 0;
    {
        return List<T>::s_min[recursive](this, 1, List<T>::s_unsafe_get(0));
    }

    method {when T KeyType} tryMin(): KeyType? {
        return List<T>::s_size(this) != 0 ? List<T>::s_min[recursive](this, 1, List<T>::s_unsafe_get(0)) : none;
    }

    method {when T KeyType} max(): KeyType 
        requires release List<T>::s_size(this) != 0;
    {
        return List<T>::s_max[recursive](this, 1, List<T>::s_unsafe_get(0));
    }

    method {when T KeyType} tryMax(): KeyType? {
        return s != e ? List<T>::s_max[recursive](this, 1, List<T>::s_unsafe_get(0)) : none;
    }

    method {when T Int | BigInt} sum(): T {
        return List<T>::s_sum[recursive](this, 0, 0, List<T>::s_size(this));
    }

    recursive? method find(p: recursive? fn(_: T) -> Bool): T {
        let idx = List<T>::s_indexof[recursive](this, 0, List<T>::s_size(this), p);
        check idx != List<T>::s_size(this);
        return List<T>::s_unsafe_get(this, idx);
    }

    recursive? method tryFind(p: recursive? fn(_: T) -> Bool): T? {
        let idx = List<T>::s_indexof[recursive](this, 0, List<T>::s_size(this), p);
        return idx != List<T>::s_size(this) ? List<T>::s_unsafe_get(this, idx) : none;
    }

    recursive? method findNot(p: recursive? fn(_: T) -> Bool): T {
        let idx = List<T>::s_indexofnot[recursive](this, 0, List<T>::s_size(this), p);
        check idx != List<T>::s_size(this);
        return List<T>::s_unsafe_get(this, idx);
    }

    recursive? method tryFindNot(p: recursive? fn(_: T) -> Bool): T? {
        let idx = List<T>::s_indexofnot[recursive](this, 0, List<T>::s_size(this), p);
        return idx != List<T>::s_size(this) ? List<T>::s_unsafe_get(this, idx) : none;
    }

    recursive? method findLast(p: recursive? fn(_: T) -> Bool): T {
        let idxlast = List<T>::s_indexoflast[recursive](this, -1, List<T>::s_size(this) - 1, p);
        check idxlast != -1;
        return List<T>::s_unsafe_get(this, idxlast);
    }

    recursive? method tryFindLast(p: recursive? fn(_: T) -> Bool): T? {
        let idxlast = List<T>::s_indexoflast[recursive](this, -1, List<T>::s_size(this) - 1, p);
        return idxlast != -1 ? List<T>::s_unsafe_get(this, idxlast) : none;
    }

    recursive? method findLastNot(p: recursive? fn(_: T) -> Bool): T {
        let idxlast = List<T>::s_indexoflastnot[recursive](this, -1, List<T>::s_size(this) - 1, p);
        check idxlast != -1;
        return List<T>::s_unsafe_get(this, idxlast);
    }

    recursive? method tryFindLastNot(p: recursive? fn(_: T) -> Bool): T? {
        let idxlast = List<T>::s_indexoflastnot[recursive](this, -1, List<T>::s_size(this) - 1, p);
        return idxlast != -1 ? List<T>::s_unsafe_get(this, idxlast) : none;
    }

    recursive? method findIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof[recursive](this, s, e, p);
        check idx != e;
        return idx;
    }

    recursive? method tryFindIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof[recursive](this, s, e, p);
        return idx != e ? idx : none;
    }

    recursive? method findIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexofnot[recursive](this, s, e, p);
        check idx != e;
        return idx;
    }

    recursive? method tryFindIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexofnot[recursive](this, s, e, p);
        return idx != e ? idx : none;
    }

    recursive? method findLastIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflast[recursive](this, s, e, p);
        check idxlast != s;
        return idxlast;
    }

    recursive? method tryFindLastIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflast[recursive](this, s, e, p);
        return idxlast != s ? idxlast : none;
    }

    recursive? method findLastIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflastnot[recursive](this, s, e, p);
        check idxlast != s;
        return idxlast;
    }

    recursive? method tryFindLastIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflastnot[recursive](this, s, e, p);
        return idxlast != s ? idxlast : none;
    }

    method {when T KeyType} indexOf(v: T, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof_keytype[recursive](this, s, e, v);
        check idx != e;
        return idx;
    }

    method {when T KeyType} tryIndexOf(v: T, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof_keytype[recursive](this, s, e, v);
        return idx != e ? idx : none;
    }

    method {when T KeyType} lastIndexOf(v: T, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idx = List<T>::s_indexoflast_keytype[recursive](this, s, e, v);
        check idx != s;
        return idx;
    }

    method {when T KeyType} tryLastIndexOf(v: T, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idx = List<T>::s_indexoflast_keytype[recursive](this, s, e, v);
        return idx != s ? idx : none;
    }

    recursive? method filter(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_filter[recursive](this, List<T>@{}, 0, p);
    }

    recursive? method filterNot(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_filternot[recursive](this, List<T>@{}, 0, p);
    }

    method ofType<U>(): List<U> {
        return List<T>::s_oftype<U>[recursive](this, List<T>@{}, 0);
    }

    method cast<U>(): List<U> {
        return List<T>::s_cast<U>[recursive](this, List<U>@{}, 0);
    }

    method slice(start?: Int, end?: Int): List<T> 
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        if(s == e) {
            return List<T>@{};
        }
        else {
            return List<T>::s_slice[recursive](this, List<T>@{}, s, e);
        }
    }

    method removeFirst(k: Int): List<T> {
        if(k >= List<T>::s_size(this)) {
            return List<T>@{};
        }
        else {
            return List<T>::s_slice[recursive](this, List<T>@{}, k, List<T>::s_size(this));
        }
    }

    method removeLast(k: Int): List<T> {
        if(k >= List<T>::s_size(this)) {
            return List<T>@{};
        }
        else {
            return List<T>::s_slice[recursive](this, List<T>@{}, 0, List<T>::s_size(this) - k);
        }
    }

    recursive? method takeWhile(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_takewhile[recursive](this, List<T>@{}, 0, p);
    }

    recursive? method discardWhile(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_discardwhile[recursive](this, 0, p);
    }

    recursive? method takeUntil(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_takeuntil[recursive](this, List<T>@{}, 0, p);
    }

    recursive? method discardUntil(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_discarduntil[recursive](this, 0, p);
    }

    method {when T KeyType} unique(): List<T> {
        return List<T>::s_unique[recursive](this, List<T>@{}, 0);
    }

    method reverse(): List<T> {
        return List<T>::s_reverse[recursive](this, List<T>@{}, 0);
    }

    recursive? method map<U>(f: recursive? fn(_: T) -> U): List<U> {
        return List<T>::s_map[recursive](this, List<U>@{}, 0, f);
    }

    recursive? method mapIndex<U>(f: recursive? fn(_: Int, _: T) -> U): List<U> {
        return List<T>::s_mapIndex[recursive](this, List<U>@{}, 0, f);
    }

    method {when T KeyType} project<U>(m: Map<T, U>): List<U> {
        return List<T>::s_projectWMap<U>[recursive](this, List<U>@{}, 0, m); 
    }

    method {when T KeyType} tryProject<U>(m: Map<T, U>): List<U?> {
        return List<T>::s_tryProjectWMap<U>[recursive](this, List<U>@{}, 0, m);
    }

    method zipIndex(): List<[Int, T]> {
        return List<T>::s_zipIndex[recursive](this, List<[Int, T]>@{}, 0);
    }

    recursive? method join<U>(ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> {
        return List<T>::s_join<U>[recursive](this, ol, List<[T, U]>@{}, 0, p);
    }

    recursive? method joinGroup<U>(ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, List<U>]> {
        return List<T>::s_joinGroup<U>[recursive](this, ol, List<[T, List<U>]>@{}, 0, p);
    }

    method append(l: List<T>): List<T> {
        let cl = List<T>::s_append[recursive](this, List<T>@{}, 0);
        return List<T>::s_append[recursive](l, cl, 0);
    }

    method prepend(l: List<T>): List<T> {
        let cl = List<T>::s_append[recursive](l, List<T>@{}, 0);
        return List<T>::s_append[recursive](this, cl, 0);
    }

    recursive? method partition<K where KeyType>(pf: recursive? fn(_: T) -> K): Map<K, List<T>> {
        return List<T>::s_partition<K>[recursive](this, Map<K, List<T>>@{}, 0, pf);
    }

    recursive? method sort(cmp: recursive? fn(_: T, _: T) -> Bool): List<T> {
        return List<T>::s_sort[recursive](this, List<T>@{}, 0, pf);
    }

    recursive? method toMap<K where KeyType, V>(kf: recursive? fn(_: T) -> K, vf: recursive? fn(_: T) -> V, merge?: Bool): Map<K, V> {
        return List<T>::s_toMap<K, V>[recursive](this, Map<K, V>@{}, 0, kf, vf, Truthy::truth(merge));
    }

    recursive? method toIndexMap<V>(vf: recursive? fn(_: Int, _: T) -> V): Map<Int, V> {
        return List<T>::s_toIndexMap<K, V>[recursive](this, Map<K, V>@{}, 0, vf);
    }
}

concept ListUtil {
    hidden recursive static s_zip<T>(l1: List<T>, l2: List<U>, lp: List<[T, U]>, idx: Int): List<[T, U]> {
        if(idx == List<T>::s_size(l1)) {
            return lp;
        }
        else {
            let vt = List<T>::s_unsafe_get(l1, idx);
            let vu = List<U>::s_unsafe_get(l2, idx);
            return ListUtil::s_zip(l1, l2, List<[T, U]>::s_unsafe_push(lp, [vt, vu]), idx + 1);
        }
    }

    hidden recursive static s_unzip<T>(l: List<[T, U]>, lt: List<T>, lu: List<U>, idx: Int): List<T>, List<U> {
        if(idx == List<T>::s_size(l)) {
            return lt, lu;
        }
        else {
            let vp = List<[T, U]>::s_unsafe_get(l, idx);
            return ListUtil::s_unzip(l, List<T>::s_unsafe_push(lt, vp.0), List<U>::s_unsafe_push(lu, vp.1), idx + 1);
        }
    }

    hidden recursive static s_concat<T>(l: List<List<T>>, lp: List<T>, idx: Int): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let ll = List<List<T>>::s_unsafe_get(l, idx);
            let cc = List<T>::s_append(ll, lp, 0);
            return List<T>::s_concat(l, lp, idx + 1);
        }
    }

    hidden recursive static s_fill<T>(k: Int, val: T, lp: List<T>): List<T> {
        if(k == 0) {
            return lp;
        }
        else {
            return ListUtil::s_fill[recursive](k - 1, List<Int>::s_unsafe_push(lp, val));
        }
    }

    hidden recursive static s_range(start: Int, end: Int, lp: List<Int>): List<Int> {
        if(start == end) {
            return lp;
        }
        else {
            return ListUtil::s_range[recursive](start + 1, end, List<Int>::s_unsafe_push(lp, start));
        }
    }

    static zip<T>(l1: List<T>, l2: List<U>): List<[T, U]>
        requires release List<T>::s_size(l1) == List<U>::s_size(l2);
    {
        return ListUtil::s_zip<T>[recursive](l1, l2, List<[T, U]>@{}, 0);
    }

    static unzip<T, U>(l: List<[T, U]>): List<T>, List<U> {
        return ListUtil::s_unzip<T>[recursive](l, List<T>@{}, List<U>@{}, 0);
    }

    static concat<T>(...ll: List<List<T>>): List<T> {
        return ListUtil::s_concat[recursive](ll, List<T>@{}, 0);
    }

    static fill<T>(k: Int, val: T): List<T>
        requires release k >= 0;
    {
        return ListUtil::s_fill<T>[recursive](k, val, List<T>@{});
    }

    static range(start: Int, end: Int): List<Int>
        requires release end > start;
    {
        return ListUtil::s_range[recursive](start, end, List<Int>@{});
    }
}
