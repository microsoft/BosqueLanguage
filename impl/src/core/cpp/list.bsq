//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

entity List<T> provides Object, Expandoable<T>, PODType when T PODType, APIType when T APIType {
    hidden static s_size(l: List<T>): Int # list_size
    hidden static s_unsafe_get(l: List<T>, i: Int): T # list_unsafe_get

    hidden static {when T KeyType} s_toset(l: List<T>): Set<T> # list_toset

    hidden recursive? static s_all(l: List<T>, p: recursive? fn(_: T) -> Bool): Bool # list_all
    hidden recursive? static s_none(l: List<T>, p: recursive? fn(_: T) -> Bool): Bool # list_none
    hidden recursive? static s_any(l: List<T>, p: recursive? fn(_: T) -> Bool): Bool # list_any

    hidden recursive? static s_count(l: List<T>, p: recursive? fn(_: T) -> Bool): Int # list_count
    hidden recursive? static s_countnot(l: List<T>, p: recursive? fn(_: T) -> Bool): Int # list_countnot

    hidden recursive? static s_indexof(l: List<T>, s: Int, e: Int, p: recursive? fn(_: T) -> Bool): Int # list_indexof
    hidden recursive? static s_indexoflast(l: List<T>, s: Int, e: Int, p: recursive? fn(_: T) -> Bool): Int # list_indexoflast

    hidden recursive? static s_indexofnot(l: List<T>, s: Int, e: Int, p: recursive? fn(_: T) -> Bool): Int # list_indexofnot
    hidden recursive? static s_indexoflastnot(l: List<T>, s: Int, e: Int, p: recursive? fn(_: T) -> Bool): Int # list_indexoflastnot

    hidden recursive? static {when T KeyType} s_count_keytype(l: List<T>, v: T): Int # list_count_keytype
    hidden recursive? static {when T KeyType} s_indexof_keytype(l: List<T>, s: Int, e: Int, v: T): Int # list_indexof_keytype
    hidden recursive? static {when T KeyType} s_indexoflast_keytype(l: List<T>, s: Int, e: Int, v: T): Int # list_indexoflast_keytype

    hidden recursive? static {when T KeyType} s_min(l: List<T>): T # list_min
    hidden recursive? static {when T KeyType} s_max(l: List<T>): T # list_max
    hidden recursive? static {when T Int | BigInt} s_sum(l: List<T>): T # list_sum

    hidden recursive? static s_filter(l: List<T>, p: recursive? fn(_: T) -> Bool): List<T> # list_filter
    hidden recursive? static s_filternot(l: List<T>, p: recursive? fn(_: T) -> Bool): List<T> # list_filternot

    hidden recursive? static s_oftype<U>(l: List<T>): List<U> # list_oftype
    hidden recursive? static s_cast<U>(l: List<T>): List<U> # list_cast
    hidden recursive? static s_slice(l: List<T>, s: Int, e: Int): List<T> # list_slice

    hidden recursive? static s_takewhile(l: List<T>, p: recursive? fn(_: T) -> Bool): List<T> # list_takewhile
    hidden recursive? static s_discardwhile(l: List<T>, p: recursive? fn(_: T) -> Bool): List<T> # list_discardwhile
    hidden recursive? static s_takeuntil(l: List<T>, p: recursive? fn(_: T) -> Bool): List<T> # list_takeuntil
    hidden recursive? static s_discarduntil(l: List<T>, p: recursive? fn(_: T) -> Bool): List<T> # list_discarduntil

    hidden static {when T KeyType} s_unique(l: List<T>): List<T> # list_unique
    hidden static s_reverse(l: List<T>): List<T> # list_reverse
    
    hidden recursive? static s_map<U>(l: List<T>, f: recursive? fn(_: T) -> U): List<U> # list_map
    hidden recursive? static s_mapindex<U>(l: List<T>, f: recursive? fn(_: Int, _: T) -> U): List<U> # list_mapindex

    hidden static {when T KeyType} s_project<U>(l: List<T>, m: Map<T, U>): List<U> # list_project
    hidden static {when T KeyType} s_tryproject<U>(l: List<T>, m: Map<T, U>): List<U?> # list_tryproject

    hidden static  s_zipindex(l: List<T>): List<[Int, T]> # list_zipindex

    hidden recursive? static s_join<U>(l: List<T>, ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> # list_join
    hidden recursive? static s_joingroup<U>(l: List<T>, ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, List<U>]> # list_joingroup

    hidden static s_append(l: List<T>, lp: List<T>): List<T> # list_append

    hidden recursive? static s_partition<K where KeyType>(l: List<T>, pf: recursive? fn(_: T) -> K): Map<K, List<T>> # list_partition
    hidden recursive? static s_sort(l: List<T>, cmp: recursive? fn(_: T, _: T) -> Bool): List<T> # list_sort

    hidden recursive? static s_toMap<K where KeyType, V>(l: List<T>, kf: recursive? fn(_: T) -> K, vf: recursive? fn(_: T) -> V, merge: Bool): Map<K, V> # list_tomap
    hidden recursive? static s_toIndexMap<V>(l: List<T>, vf: recursive? fn(_: Int, _: T) -> V): Map<Int, V> # list_toindexmap

    hidden static s_rangeInBoundsFwd(l: List<T>, start?: Int, end?: Int): Bool {
        let s, e = start ?| 0, end ?| List<T>::s_size(l);
        return 0 <= s && s <= e && e <= List<T>::s_size(l);
    }

    hidden static s_rangeInBoundsRev(l: List<T>, start?: Int, end?: Int): Bool {
        let s, e = start ?| -1, end ?| List<T>::s_size(l) - 1;
        return -1 <= s && s <= e && e <= List<T>::s_size(l) - 1;
    }

    method size(): Int {
        return List<T>::s_size(this);
    }

    method empty(): Int {
        return List<T>::s_size(this) == 0;
    }

    method front(): T 
        requires release List<T>::s_size(this) != 0;
    {
        return List<T>::s_unsafe_get(this, 0);
    }

    method tryFront(): T? {
        return List<T>::s_size(this) != 0 ? List<T>::s_unsafe_get(this, 0) : none;
    }

    method last(): T 
        requires release List<T>::s_size(this) != 0;
    {
        List<T>::s_unsafe_get(this, List<T>::s_size(this) - 1);
    }

    method tryLast(): T? {
        return List<T>::s_size(this) != 0 ? List<T>::s_unsafe_get(this, List<T>::s_size(this) - 1) : none;
    }

    method get(i: Int): T 
        requires release 0 <= idx && idx < List<T>::s_size(this);
    {
        return List<T>::s_unsafe_get(this, idx);
    }

    method tryGet(i: Int): T? {
        return 0 <= idx && idx < List<T>::s_size(this) ? List<T>::s_unsafe_get(this, idx) : none;
    }

    method {when T KeyType} toSet(): Set<T> {
        return List<T>::s_toset(this);
    }

    recursive? method single(p: recursive? fn(_: T) -> Bool): Bool 
        requires release List<T>::s_size(this) != 0;
    {
        let idx = List<T>::s_indexof[recursive?](this, 0, List<T>::s_size(this), p);
        let lastidx = List<T>::s_indexoflast[recursive?](this, -1, List<T>::s_size(this) - 1, p);

        check lastidx == idx;
        return List<T>::s_unsafe_get(this, idx);
    }

    recursive? method trySingle(p: recursive? fn(_: T) -> Bool): T? {
        let idx = List<T>::s_indexof[recursive?](this, 0, List<T>::s_size(this), p);
        let lastidx = List<T>::s_indexoflast[recursive?](this, -1, List<T>::s_size(this) - 1, p);

        return idx == lastidx ? List<T>::s_unsafe_get(this, idx) : none;
    }

    method uniform(i: Int, start?: Int, end?: Int): T
        requires release List<T>::s_size(this) != 0;
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        abort;
    }

    recursive? method allof(p: recursive? fn(_: T) -> Bool): Bool {
        return List<T>::s_size(this) == 0 || List<T>::s_all[recursive?](this, p);
    }

    recursive? method noneof(p: recursive? fn(_: T) -> Bool): Bool {
        return List<T>::s_size(this) == 0 || List<T>::s_none[recursive?](this, 0, List<T>::s_size(this), p);
    }

    recursive? method anyof(p: recursive? fn(_: T) -> Bool): Bool {
        return List<T>::s_size(this) != 0 && List<T>::s_any[recursive?](this, 0, List<T>::s_size(this), p);
    }

    recursive? method countIf(p: recursive? fn(_: T) -> Bool): Int {
        return List<T>::s_count[recursive?](this, p);
    }

    recursive? method countIfNot(p: recursive? fn(_: T) -> Bool): Int {
        return List<T>::s_countnot[recursive?](this, p);
    }

    method {when T KeyType} contains(v: T): Bool {
        return List<T>::s_indexof_keytype(this, 0, List<T>::s_size(this), v) != List<T>::s_size(this);
    }

    method {when T KeyType} count(v: T): Int {
        return List<T>::s_count_keytype(this, v);
    }

    method {when T KeyType} min(): T 
        requires release List<T>::s_size(this) != 0;
    {
        return List<T>::s_min(this);
    }

    method {when T KeyType} tryMin(): T? {
        return List<T>::s_size(this) != 0 ? List<T>::s_min(this) : none;
    }

    method {when T KeyType} max(): T 
        requires release List<T>::s_size(this) != 0;
    {
        return List<T>::s_max(this);
    }

    method {when T KeyType} tryMax(): T? {
        return s != e ? List<T>::s_max(this) : none;
    }

    method {when T Int | BigInt} sum(): T {
        return List<T>::s_sum(this);
    }

    recursive? method find(p: recursive? fn(_: T) -> Bool): T {
        let idx = List<T>::s_indexof[recursive?](this, 0, List<T>::s_size(this), p);
        check idx != List<T>::s_size(this);
        return List<T>::s_unsafe_get(this, idx);
    }

    recursive? method tryFind(p: recursive? fn(_: T) -> Bool): T? {
        let idx = List<T>::s_indexof[recursive?](this, 0, List<T>::s_size(this), p);
        return idx != List<T>::s_size(this) ? List<T>::s_unsafe_get(this, idx) : none;
    }

    recursive? method findNot(p: recursive? fn(_: T) -> Bool): T {
        let idx = List<T>::s_indexofnot[recursive?](this, 0, List<T>::s_size(this), p);
        check idx != List<T>::s_size(this);
        return List<T>::s_unsafe_get(this, idx);
    }

    recursive? method tryFindNot(p: recursive? fn(_: T) -> Bool): T? {
        let idx = List<T>::s_indexofnot[recursive?](this, 0, List<T>::s_size(this), p);
        return idx != List<T>::s_size(this) ? List<T>::s_unsafe_get(this, idx) : none;
    }

    recursive? method findLast(p: recursive? fn(_: T) -> Bool): T {
        let idxlast = List<T>::s_indexoflast[recursive?](this, -1, List<T>::s_size(this) - 1, p);
        check idxlast != -1;
        return List<T>::s_unsafe_get(this, idxlast);
    }

    recursive? method tryFindLast(p: recursive? fn(_: T) -> Bool): T? {
        let idxlast = List<T>::s_indexoflast[recursive?](this, -1, List<T>::s_size(this) - 1, p);
        return idxlast != -1 ? List<T>::s_unsafe_get(this, idxlast) : none;
    }

    recursive? method findLastNot(p: recursive? fn(_: T) -> Bool): T {
        let idxlast = List<T>::s_indexoflastnot[recursive?](this, -1, List<T>::s_size(this) - 1, p);
        check idxlast != -1;
        return List<T>::s_unsafe_get(this, idxlast);
    }

    recursive? method tryFindLastNot(p: recursive? fn(_: T) -> Bool): T? {
        let idxlast = List<T>::s_indexoflastnot[recursive?](this, -1, List<T>::s_size(this) - 1, p);
        return idxlast != -1 ? List<T>::s_unsafe_get(this, idxlast) : none;
    }

    recursive? method findIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof[recursive?](this, s, e, p);
        check idx != e;
        return idx;
    }

    recursive? method tryFindIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof[recursive?](this, s, e, p);
        return idx != e ? idx : none;
    }

    recursive? method findIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexofnot[recursive?](this, s, e, p);
        check idx != e;
        return idx;
    }

    recursive? method tryFindIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexofnot[recursive?](this, s, e, p);
        return idx != e ? idx : none;
    }

    recursive? method findLastIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflast[recursive?](this, s, e, p);
        check idxlast != s;
        return idxlast;
    }

    recursive? method tryFindLastIndexOf(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflast[recursive?](this, s, e, p);
        return idxlast != s ? idxlast : none;
    }

    recursive? method findLastIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflastnot[recursive?](this, s, e, p);
        check idxlast != s;
        return idxlast;
    }

    recursive? method tryFindLastIndexOfNot(p: recursive? fn(_: T) -> Bool, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idxlast = List<T>::s_indexoflastnot[recursive?](this, s, e, p);
        return idxlast != s ? idxlast : none;
    }

    method {when T KeyType} indexOf(v: T, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof_keytype(this, s, e, v);
        check idx != e;
        return idx;
    }

    method {when T KeyType} tryIndexOf(v: T, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        let idx = List<T>::s_indexof_keytype(this, s, e, v);
        return idx != e ? idx : none;
    }

    method {when T KeyType} lastIndexOf(v: T, start?: Int, end?: Int): Int
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idx = List<T>::s_indexoflast_keytype(this, s, e, v);
        check idx != s;
        return idx;
    }

    method {when T KeyType} tryLastIndexOf(v: T, start?: Int, end?: Int): Int?
        requires release this->s_rangeInBoundsRev(this, start, end);
    {
        let s, e = start ?| -1, end ?| List<T>::s_size(this) - 1;

        let idx = List<T>::s_indexoflast_keytype(this, s, e, v);
        return idx != s ? idx : none;
    }

    recursive? method filter(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_filter[recursive?](this, p);
    }

    recursive? method filterNot(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_filternot[recursive?](this, p);
    }

    method ofType<U>(): List<U> {
        return List<T>::s_oftype<U>(this);
    }

    method cast<U>(): List<U> {
        return List<T>::s_cast<U>(this);
    }

    method slice(start?: Int, end?: Int): List<T> 
        requires release this->s_rangeInBoundsFwd(this, start, end);
    {
        let s, e = start ?| 0, end ?| List<T>::s_size(this);

        if(s == e) {
            return List<T>@{};
        }
        else {
            return List<T>::s_slice(this, s, e);
        }
    }

    method removeFirst(k: Int): List<T> {
        if(k >= List<T>::s_size(this)) {
            return List<T>@{};
        }
        else {
            return List<T>::s_slice(this, k, List<T>::s_size(this));
        }
    }

    method removeLast(k: Int): List<T> {
        if(k >= List<T>::s_size(this)) {
            return List<T>@{};
        }
        else {
            return List<T>::s_slice(this, 0, List<T>::s_size(this) - k);
        }
    }

    recursive? method takeWhile(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_takewhile[recursive?](this, p);
    }

    recursive? method discardWhile(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_discardwhile[recursive?](this, p);
    }

    recursive? method takeUntil(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_takeuntil[recursive?](this, p);
    }

    recursive? method discardUntil(p: recursive? fn(_: T) -> Bool): List<T> {
        return List<T>::s_discarduntil[recursive?](this, p);
    }

    method {when T KeyType} unique(): List<T> {
        return List<T>::s_unique(this);
    }

    method reverse(): List<T> {
        return List<T>::s_reverse(this);
    }

    recursive? method map<U>(f: recursive? fn(_: T) -> U): List<U> {
        return List<T>::s_map[recursive?](this, f);
    }

    recursive? method mapIndex<U>(f: recursive? fn(_: Int, _: T) -> U): List<U> {
        return List<T>::s_mapIndex[recursive?](this, f);
    }

    method {when T KeyType} project<U>(m: Map<T, U>): List<U> {
        return List<T>::s_projectWMap<U>(this, m); 
    }

    method {when T KeyType} tryProject<U>(m: Map<T, U>): List<U?> {
        return List<T>::s_tryProjectWMap<U>(this, m);
    }

    method zipIndex(): List<[Int, T]> {
        return List<T>::s_zipIndex(this);
    }

    recursive? method join<U>(ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, U]> {
        return List<T>::s_join<U>[recursive?](this, ol, p);
    }

    recursive? method joinGroup<U>(ol: List<U>, p: recursive? fn(_: T, _: U) -> Bool): List<[T, List<U>]> {
        return List<T>::s_joinGroup<U>[recursive?](this, ol, p);
    }

    method append(l: List<T>): List<T> {
        return List<T>::s_append(this, l);
    }

    method prepend(l: List<T>): List<T> {
        return List<T>::s_append(l, this);
    }

    recursive? method partition<K where KeyType>(pf: recursive? fn(_: T) -> K): Map<K, List<T>> {
        return List<T>::s_partition<K>[recursive?](this, pf);
    }

    recursive? method sort(cmp: recursive? fn(_: T, _: T) -> Bool): List<T> {
        return List<T>::s_sort[recursive?](this, pf);
    }

    recursive? method toMap<K where KeyType, V>(kf: recursive? fn(_: T) -> K, vf: recursive? fn(_: T) -> V, merge?: Bool): Map<K, V> {
        return List<T>::s_toMap<K, V>[recursive?](this, kf, vf, Truthy::truth(merge));
    }

    recursive? method toIndexMap<V>(vf: recursive? fn(_: Int, _: T) -> V): Map<Int, V> {
        return List<T>::s_toIndexMap<K, V>[recursive?](this, vf);
    }
}

concept ListUtil {
    hidden recursive static s_zip<T>(l1: List<T>, l2: List<U>, lp: List<[T, U]>, idx: Int): List<[T, U]> {
        if(idx == List<T>::s_size(l1)) {
            return lp;
        }
        else {
            let vt = List<T>::s_unsafe_get(l1, idx);
            let vu = List<U>::s_unsafe_get(l2, idx);
            return ListUtil::s_zip(l1, l2, List<[T, U]>::s_unsafe_push(lp, [vt, vu]), idx + 1);
        }
    }

    hidden recursive static s_unzip<T>(l: List<[T, U]>, lt: List<T>, lu: List<U>, idx: Int): List<T>, List<U> {
        if(idx == List<T>::s_size(l)) {
            return lt, lu;
        }
        else {
            let vp = List<[T, U]>::s_unsafe_get(l, idx);
            return ListUtil::s_unzip(l, List<T>::s_unsafe_push(lt, vp.0), List<U>::s_unsafe_push(lu, vp.1), idx + 1);
        }
    }

    hidden recursive static s_concat<T>(l: List<List<T>>, lp: List<T>, idx: Int): List<T> {
        if(idx == List<T>::s_size(l)) {
            return lp;
        }
        else {
            let ll = List<List<T>>::s_unsafe_get(l, idx);
            let cc = List<T>::s_append(ll, lp, 0);
            return List<T>::s_concat(l, lp, idx + 1);
        }
    }

    hidden recursive static s_fill<T>(k: Int, val: T, lp: List<T>): List<T> {
        if(k == 0) {
            return lp;
        }
        else {
            return ListUtil::s_fill[recursive](k - 1, List<Int>::s_unsafe_push(lp, val));
        }
    }

    hidden recursive static s_fillIndex(k: Int, lp: List<Int>, idx: Int): List<Int> {
        if(idx == k) {
            return lp;
        }
        else {
            return ListUtil::s_fillIndex[recursive](k, List<Int>::s_unsafe_push(lp, idx), idx + 1);
        }
    }

    hidden recursive static s_range(start: Int, end: Int, lp: List<Int>): List<Int> {
        if(start == end) {
            return lp;
        }
        else {
            return ListUtil::s_range[recursive](start + 1, end, List<Int>::s_unsafe_push(lp, start));
        }
    }

    static zip<T>(l1: List<T>, l2: List<U>): List<[T, U]>
        requires release List<T>::s_size(l1) == List<U>::s_size(l2);
    {
        return ListUtil::s_zip<T>[recursive](l1, l2, List<[T, U]>@{}, 0);
    }

    static unzip<T, U>(l: List<[T, U]>): List<T>, List<U> {
        return ListUtil::s_unzip<T>[recursive](l, List<T>@{}, List<U>@{}, 0);
    }

    static concat<T>(...ll: List<List<T>>): List<T> {
        return ListUtil::s_concat[recursive](ll, List<T>@{}, 0);
    }

    static fill<T>(k: Int, val: T): List<T> {
        return ListUtil::s_fill<T>[recursive](k, val, List<T>@{});
    }

    static fillIndex(k: Int): List<Int> {
        return ListUtil::s_fillIndex[recursive](k, List<Int>@{}, 0);
    }

    static range(start: Int, end: Int): List<Int>  {
        return ListUtil::s_range[recursive](start, end, List<Int>@{});
    }
}

concept ListUtil {
    hidden static s_zip<T>(l1: List<T>, l2: List<U>): List<[T, U]> # list_zip
    hidden static s_unzip<T>(l: List<[T, U]>): List<T>, List<U> # list_unzip
    hidden static s_concat<T>(l: List<List<T>>): List<T> # list_concat
    hidden static s_fill<T>(k: Int, val: T): List<T> # list_fill
    hidden static s_range(start: Int, end: Int): List<Int> # list_range

    static zip<T>(l1: List<T>, l2: List<U>): List<[T, U]>
        requires release List<T>::s_size(l1) == List<U>::s_size(l2);
    {
        return ListUtil::s_zip<T>(l1, l2);
    }

    static unzip<T, U>(l: List<[T, U]>): List<T>, List<U> {
        return ListUtil::s_unzip<T>(l);
    }

    static concat<T>(...ll: List<List<T>>): List<T> {
        return ListUtil::s_concat(ll);
    }

    static fill<T>(k: Int, val: T): List<T>
        requires release k >= 0;
    {
        return ListUtil::s_fill<T>(k, val);
    }

    static range(start: Int, end: Int): List<Int>
        requires release end > start;
    {
        return ListUtil::s_range(start, end);
    }
}
