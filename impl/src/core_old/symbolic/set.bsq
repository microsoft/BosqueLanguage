//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

entity Set<T where KeyType> provides Object, Expandoable<T>, PODType when T PODType, APIType when T APIType {
    pragma KnownSafe
    hidden static s_size(s: Set<T>): Int # set_size

    pragma KnownSafe
    hidden static s_has_key(s: Set<T>, key: T): Bool # set_has_key

    pragma KnownSafe
    hidden static s_get_keylist(s: Set<T>): KeyList<T>? # set_get_keylist

    pragma AssumeSafe
    hidden static s_unsafe_add(s: Set<T>, v: T, newkl: KeyList<T>): Set<T> # set_unsafe_add

    pragma KnownSafe
    hidden static s_add(s: Set<T>, v: T): Set<T> {
        if(Set<T>::s_has_key(s, v)) {
            return s;
        }
        else {
            let kl = Set<T>::s_get_keylist(s);
            return Set<T>::s_unsafe_add(s, v, KeyList<T>@{hkey=v, tail=kl});
        }
    }

    //we should be able to use the array map operation on the has array
    hidden static s_subsetof(s: Set<T>, other: Set<T>): Bool # set_subsetof
    hidden static s_equal(s: Set<T>, other: Set<T>): Bool # set_equal
    hidden static s_disjoint(s: Set<T>, other: Set<T>): Bool # set_disjoint

    hidden recursive static s_entry_list(kl: KeyList<T>?, l: List<T>): List<T> {
        if(kl == none) {
            return l;
        }
        else {
            return Set<T>::s_entry_list[recursive](kl.tail, List<T>::s_unsafe_push(l, kl.hkey));
        }
    }

    hidden recursive static s_hasall(s: Set<T>, l: List<T>, lsize: Int, idx: Int): Bool {
        if(idx == lsize) {
            return true;
        }
        else {
            let v = List<T>::s_unsafe_get(l, idx);
            if(!Set<T>::s_has_key(s, v)) {
                return false;
            }
            else {
                return Set<T>::s_hasall[recursive](s, l, lsize, idx + 1);
            }
        }
    }

    hidden recursive static s_subset(kl: KeyList<T>?, sp: Set<T>, p: recursive? fn(_: T) -> Bool): Set<T> {
        if(kl == none) {
            return sp;
        }
        else {
            if(p[recursive?](kl.hkey)) {
                return Set<T>::s_subset[recursive](Set<T>::s_add(sp, kl.hkey), kl.tail, p);
            }
            else {
                return Set<T>::s_subset[recursive](sp, kl.tail, p);
            }
        }
    }

    hidden recursive static s_oftype<U where KeyType>(kl: KeyList<T>?, sp: Set<U>): Set<U> {
        if(kl == none) {
            return sp;
        }
        else {
            if(kl.hkey.is<U>()) {
                return Set<T>::s_oftype[recursive](Set<U>::s_add(sp, kl.hkey.as<U>()), kl.tail);
            }
            else {
                return Set<T>::s_oftype[recursive](sp, kl.tail, p);
            }
        }
    }

    hidden recursive static s_cast<U where KeyType>(kl: KeyList<T>?, sp: Set<U>): Set<U> {
        if(kl == none) {
            return sp;
        }
        else {
            return Set<T>::s_cast[recursive](Set<U>::s_add(sp, kl.hkey.as<U>()), kl.tail);
        }
    }

    hidden recursive static s_union(sp: Set<T>, kl: KeyList<T>?): Set<T> {
        if(kl == none) {
            return sp;
        }
        else {
            return Set<T>::s_union[recursive](Set<T>::s_add(sp, kl.hkey), kl.tail);
        }
    }

    hidden recursive static s_intersect(s: Set<T>, sp: Set<T>, kl: KeyList<T>?): Set<T> {
        if(kl == none) {
            return sp;
        }
        else {
            if(!Set<T>::s_has_key(s, kl.hkey)) {
                return Set<T>::s_intersect(s, sp, kl.tail);
            }
            else {
                return Set<T>::s_intersect[recursive](s, Set<T>::s_add(sp, kl.hkey), kl.tail);
            }
        }
    }

    hidden recursive static s_diff(os: Set<T>, sp: Set<T>, kl: KeyList<T>?): Set<T> {
        if(kl == none) {
            return sp;
        }
        else {
            if(Set<T>::s_has_key(os, kl.hkey)) {
                return Set<T>::s_diff[recursive](os, sp, kl.tail);
            }
            else {
                return Set<T>::s_diff[recursive](os, Set<T>::s_add(sp, kl.hkey), kl.tail);
            }
        }
    }

    hidden recursive static s_unionall(sl: List<Set<T>>, slsize: Int, sp: Set<T>, idx: Int): Set<T> {
        if(idx == slsize) {
            return sp;
        }
        else {
            let s = List<T>::s_unsafe_get(sl, idx);
            return Set<T>::s_unionall[recursive](sl, slsize, sp.union(s), idx + 1);
        }
    }

    hidden recursive static s_intersectall(sl: List<Set<T>>, slsize: Int, sp: Set<T>, idx: Int): Set<T> {
        if(idx == List<T>::s_size(sl)) {
            return sp;
        }
        else {
            let s = List<T>::s_unsafe_get(sl, idx);
            return Set<T>::s_intersectall[recursive](sl, slsize, sp.intersect(s), idx + 1);
        }
    }

    method size(): Int {
        return Set<T>::s_size(this);
    }

    method empty(): Bool {
        return Set<T>::s_size(this) == 0;
    }

    method entries(): List<T> {
        return Set<T>::s_entry_list(KeyList<K>::sort(Set<T>::s_get_keylist(this)), List<T>@{});
    }

    method has(v: T): Bool {
        return Set<T>::s_has_key(this, v);
    }

    method hasAll(...v: List<T>): Bool {
        return Set<T>::s_hasall[recursive](this, List<T>::s_size(v), v, 0);
    }

    method toDynamicSet<T>(): DynamicSet<T> {
        abort;
    }

    method subsetOf(s: Set<T>): Bool {
        if(Set<T>::s_size(s) > Set<T>::s_size(this)) {
            return false;
        }
        else {
            return Set<T>::s_subsetof(this, s);
        }
    }

    method equal(s: Set<T>): Bool {
        if(Set<T>::s_size(this) != Set<T>::s_size(s)) {
            return false;
        }
        else {
            return Set<T>::s_equal(this, s);
        }
    }

    method disjoint(s: Set<T>): Bool {
        return Set<T>::s_disjoint(this, s);
    }

    recursive? method subset(p: recursive? fn(_: T) -> Bool): Set<T> {
        return Set<T>::s_subset[recursive](Set<T>::s_get_keylist(this), Set<T>@{}, p);
    }

    method ofType<U where KeyType>(): Set<U> {
        return Set<T>::s_oftype<U>[recursive](Set<T>::s_get_keylist(this), Set<U>@{}, 0);
    }

    method cast<U where KeyType>(): Set<U> {
        return Set<T>::s_cast<U>[recursive](Set<T>::s_get_keylist(this), Set<U>@{}, 0);
    }

    method union(s: Set<T>): Set<T> {
        return Set<T>::s_union[recursive](this, Set<T>::s_get_keylist(s));
    }

    method intersect(s: Set<T>): Set<T> {
        return Set<T>::s_intersect[recursive](s, Set<T>@{}, Set<T>::s_get_keylist(this));
    }

    method difference(s: Set<T>): Set<T> {
        return Set<T>::s_diff[recursive](s, Set<T>@{}, Set<T>::s_get_keylist(this));
    }

    method symmetricDifference(s: Set<T>): Set<T> {
        let d1 = this.diffWith(s);
        let d2 = s.diffWith(this);
        return d1.unionWith(d2);
    }

    static unionOf(...sl: List<Set<T>>): Set<T> {
        if(List<T>::s_size(sl) == 0) {
            return Set<T>@{};
        }
        elif(List<T>::s_size(sl) == 1) {
            return List<T>::s_unsafe_get(sl, 0);
        }
        else {
            return Set<T>::s_unionall(sl, List<Set<T>>::s_size(sl), Set<T>@{}, 0);
        }
    }

    static intersectionOf(...sl: List<Set<T>>): Set<T> {
        if(List<T>::s_size(sl) == 0) {
            return Set<T>@{};
        }
        elif(List<T>::s_size(sl) == 1) {
            return List<T>::s_unsafe_get(sl, 0);
        }
        else {
            return Set<T>::s_intersectall(sl, List<Set<T>>::s_size(sl), Set<T>@{}, 0);
        }
    }
}

entity DynamicSet<T where KeyType> provides Object, Expandoable<T> {
    method size(): Int {
        abort;
    }

    method empty(): Bool {
        abort;
    }

    method entries(): List<T> {
        abort;
    }

    method has(v: T): Bool  {
        abort;
    }

    method hasAll(...v: List<T>): Bool  {
        abort;
    }

    method toSet<T>(): Set<T> {
        abort;
    }

    method add(v: T): DynamicSet<T>  {
        abort;
    }

    method addAll(...v: List<T>): DynamicSet<T>  {
        abort;
    }

    method insert(v: T): DynamicSet<T>  {
        abort;
    }

    method insertAll(...v: List<T>): DynamicSet<T>  {
        abort;
    }

    method remove(v: T): DynamicSet<T>  {
        abort;
    }

    method removeAll(...v: List<T>): DynamicSet<T>  {
        abort;
    }

    method subsetOf(s: DynamicSet<T>): Bool  {
        abort;
    }

    method equal(s: DynamicSet<T>): Bool  {
        abort;
    }

    method disjoint(s: DynamicSet<T>): Bool  {
        abort;
    }

    recursive? method subset(p: recursive? fn(_: T) -> Bool): Set<T> {
        abort;
    }

    method ofType<U where KeyType>(): Set<U> {
        abort;
    }

    method cast<U where KeyType>(): Set<U> {
        abort;
    }

    method union(s: DynamicSet<T>): DynamicSet<T>  {
        abort;
    }

    method intersect(s: DynamicSet<T>): DynamicSet<T>  {
        abort;
    }

    method difference(s: DynamicSet<T>): DynamicSet<T>  {
        abort;
    }

    method symmetricDifference(s: DynamicSet<T>): DynamicSet<T>  {
        abort;
    }

    static unionOf(...sl: List<DynamicSet<T>>): DynamicSet<T> {
        abort;
    }

    static intersectionOf(...sl: List<DynamicSet<T>>): DynamicSet<T> {
        abort;
    }
}
