//-------------------------------------------------------------------------------------------------------
// Copyright (C) Microsoft. All rights reserved.
// Licensed under the MIT license. See LICENSE.txt file in the project root for full license information.
//-------------------------------------------------------------------------------------------------------

//The core namespace implements the primitive concepts used in the bosque core language
namespace NSCore;

entity Map<K where KeyType, V> provides Object, Expandoable<MapEntry<K, V>>, PODType when K PODType && V PODType, APIType when K APIType && V APIType {
    pragma KnownSafe
    hidden static s_size(m: Map<K, V>): Int # map_size

    pragma KnownSafe
    hidden static s_has_key(m: Map<K, V>, key: K): Bool # map_has_key

    pragma AssumeSafe
    hidden static s_at_val(m: Map<K, V>, key: K): V # map_at_val

    pragma KnownSafe
    hidden static s_get_keylist(m: Map<K, V>): KeyList<K>? # map_get_keylist
    
    pragma AssumeSafe
    hidden static s_unsafe_add(m: Map<K, V>, key: K, val: V, newkl: KeyList<K>): Map<K, V> # map_unsafe_add

    pragma AssumeSafe
    hidden static s_unsafe_update(m: Map<K, V>, key: K, val: V): Map<K, V> # map_unsafe_update

    pragma AssumeSafe
    hidden static s_add(m: Map<K, V>, k: K, v: V): Map<K, V> {
        let kl = Map<K, V>::s_get_keylist(m);
        return Map<K, V>::s_unsafe_add(m, k, v, KeyList<K>@{hkey=k, tail=kl});
    }

    pragma AssumeSafe
    hidden static s_update(m: Map<K, V>, k: K, v: V): Map<K, V> {
        return Map<K, V>::s_unsafe_update(m, k, v);
    }

    hidden recursive static s_key_list(kl: KeyList<K>?, l: List<K>): List<K> {
        if(kl == none) {
            return l;
        }
        else {
            return Map<K, V>::s_key_list[recursive](kl.tail, List<K>::s_unsafe_push(l, kl.hkey));
        }
    }

    //we should be able to take keyset and has array for the set directly
    hidden static s_key_set(m: Map<K, V>): Set<K> # map_keyset

    hidden recursive static s_domainincludes(m: Map<K, V>, kl: KeyList<K>?): Bool {
        if(kl == none) {
            return true;
        }
        else {
            if(!Map<K, V>::s_has_key(m, kl.hkey)) {
                return false;
            }
            else {
                return Map<K, V>::s_domainincludes[recursive](m, kl.tail);
            }
        }
    }

    hidden recursive static s_value_list(m: Map<K, V>, kl: KeyList<K>?, l: List<V>): List<V> {
        if(kl == none) {
            return l;
        }
        else {
            let v = Map<K, V>::s_at_val(m, kl.hkey);
            return Map<K, V>::s_value_list[recursive](m, kl.tail, List<V>::s_unsafe_push(l, v));
        }
    }

    hidden recursive static s_entry_list(m: Map<K, V>, kl: KeyList<K>?, l: List<MapEntry<K, V>>): List<MapEntry<K, V>> {
        if(kl == none) {
            return l;
        }
        else {
            let v = Map<K, V>::s_at_val(m, kl.hkey);
            return Map<K, V>::s_entry_list[recursive](m, kl.tail, List<MapEntry<K, V>>::s_unsafe_push(l, MapEntry<K, V>@{ key=kl.hkey, value=v }));
        }
    }

    hidden recursive static s_hasall(m: Map<K, V>, l: List<K>, lsize: Int, idx: Int): Bool {
        if(idx == lsize) {
            return true;
        }
        else {
            let v = List<K>::s_unsafe_get(l, idx);
            if(!Map<K, V>::s_has_key(m, v)) {
                return false;
            }
            else {
                return Map<K, V>::s_hasall[recursive](m, l, lsize, idx + 1);
            }
        }
    }

    hidden recursive static s_submap(m: Map<K, V>, kl: KeyList<K>?, mp: Map<K, V>, p: recursive? fn(_: K, _: V) -> Bool): Map<K, V> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            if(!p[recursive?](k, v)) {
                return Map<K, V>::s_submap[recursive](m, kl.tail, mp, p);
            }
            else {
                return Map<K, V>::s_submap[recursive](m, kl.tail, Map<K, V>::s_add(mp, k, v), p);
            }
        }
    }

    hidden recursive static s_oftype<T where KeyType, U>(m: Map<K, V>, kl: KeyList<K>?, mp: Map<T, U>): Map<T, U> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            if(k.is<T>() && v.is<U>()) {
                return Map<K, V>::s_oftype<T, U>[recursive](m, kl.tail, Map<T, U>::s_add(mp, k.as<T>(), v.as<U>()));
            }
            else {
                return Map<K, V>::s_oftype<T, U>[recursive](m, kl.tail, mp);
            }
        }
    }

    hidden recursive static s_cast<T where KeyType, U>(m: Map<K, V>, kl: KeyList<K>?, mp: Map<T, U>): Map<T, U> {
       if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);
            check (k.is<T>() && v.is<U>());

            return Map<K, V>::s_cast<T, U>[recursive](m, kl.tail, Map<T, U>::s_add(mp, k.as<T>(), v.as<U>()));
        }
    }

    hidden static s_projectall(m: Map<K, V>, l: List<K>): Map<K, V> {
        let lset = List<K>::s_toset[recursive](l, List<K>::s_size(l), 0, Set<K>@{});

        return Map<K, V>::s_project[recursive](m, Set<K>::s_get_keylist(lset), Map<K, V>@{});
    }

    hidden static s_excludeall(m: Map<K, V>, l: List<K>): Map<K, V> {
        let lset = List<K>::s_toset[recursive](l, List<K>::s_size(l), 0, Set<K>@{});

        return Map<K, V>::s_exclude[recursive](m, lset, Map<K, V>::s_get_keylist(m), Map<K, V>@{});
    }

    hidden recursive static s_project(m: Map<K, V>, kl: KeyList<K>?, mp: Map<K, V>): Map<K, V> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            
            if(!Map<K, V>::s_has_key(m, k)) {
                return Map<K, V>::s_project[recursive](m, kl.tail, mp);
            }
            else {
                let v = Map<K, V>::s_at_val(m, k);
                return Map<K, V>::s_project[recursive](m, kl.tail, Map<K, V>::s_add(mp, k, v));
            }
        }
    }

    hidden recursive static s_exclude(m: Map<K, V>, s: Set<K>, kl: KeyList<K>?, mp: Map<K, V>): Map<K, V> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            
            if(Set<K>::s_has_key(s, k)) {
                return Map<K, V>::s_exclude[recursive](m, s, kl.tail, mp);
            }
            else {
                let v = Map<K, V>::s_at_val(m, k);
                return Map<K, V>::s_exclude[recursive](m, s, kl.tail, Map<K, V>::s_add(mp, k, v));
            }
        }
    }

    hidden recursive static s_remap<U>(m: Map<K, V>, kl: KeyList<K>?, mp: Map<K, U>, f: recursive? fn(_: K, _: V) -> U): Map<K, U> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            let u = f[recursive?](k, v);
            return Map<K, V>::s_remap<U>[recursive](m, kl.tail, Map<K, U>::s_add(mp, k, u), f);
        }
    }

    hidden recursive static s_composewith<U>(m: Map<K, V>, kl: KeyList<K>?, mp: Map<V, U>, mc: Map<K, U>): Map<K, U> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            check Map<V, U>::s_has_key(mc, v);
            let u = Map<V, U>::s_at_val(mc, v);

            return Map<K, V>::s_composewith<U>[recursive](m, kl.tail, Map<K, U>::s_add(mp, k, u), mc);
        }
    }

    hidden recursive static s_trycomposewith<U>(m: Map<K, V>, kl: KeyList<K>?, mp: Map<K, U?>, mc: Map<K, U>): Map<K, U?> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            if(!Map<V, U>::s_has_key(mc, v)) {
                return Map<K, V>::s_trycomposewith<U>[recursive](m, kl.tail, Map<K, U?>::s_add(mp, k, none), mc);
            }
            else {
                let u = Map<V, U>::s_at_val(mc, v);   
                return Map<K, V>::s_trycomposewith<U>[recursive](m, kl.tail, Map<K, U?>::s_add(mp, k, u), mc);
            }
        }
    }

    hidden recursive static s_defaultcomposewith<U>(m: Map<K, V>, kl: KeyList<K>?, mp: Map<V, U>, mc: Map<K, U>, default: U): Map<K, U> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            if(!Map<V, U>::s_has_key(mc, v)) {
                return Map<K, V>::s_defaultcomposewith<U>[recursive](m, kl.tail, Map<K, U>::s_add(mp, k, default), mc, default);
            }
            else {
                let u = Map<V, U>::s_at_val(mc, v);   
                return Map<K, V>::s_defaultcomposewith<U>[recursive](m, kl.tail, Map<K, U>::s_add(mp, k, u), mc, default);
            }
        }
    }

    hidden recursive static s_invertinj(m: Map<K, V>, kl: KeyList<K>?, mp: Map<V, K>): Map<V, K> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            check !Map<V, K>::s_has_key(mp, v);
            return Map<K, V>::s_invertinj[recursive](m, kl.tail, Map<V, K>::s_add(mp, v, k));
        }
    }

    hidden recursive static s_invertrel_single(m: Map<K, V>, kl: KeyList<K>?, v: V, ll: List<K>): List<K> {
        if(kl == none) {
            return ll;
        }
        else {
            let k = kl.hkey;
            let vv = Map<K, V>::s_at_val(m, k);

            if(vv != v) {
                return Map<K, V>::s_invertrel_single[recursive](m, kl.tail, v, ll);
            }
            else {
                return Map<K, V>::s_invertrel_single[recursive](m, kl.tail, v, List<K>::s_unsafe_push(ll, k));
            }
        }
    }

    hidden recursive static s_invertrel(m: Map<K, V>, kl: KeyList<K>?, mp: Map<V, List<K>>): Map<V, List<K>> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            if(Map<V, List<K>>::s_has_key(mp, v)) {
                return Map<K, V>::s_invertrel[recursive](m, kl.tail, mp);
            }
            else {
                let rl = Map<K, V>::s_invertrel_single[recursive](m, kl.tail, v, List<K>@{k});
                return Map<K, V>::s_invertrel[recursive](m, kl.tail, Map<V, List<K>>::s_add(mp, v, rl));
            }
        }
    }

    hidden recursive static s_union(m: Map<K, V>, mp: Map<K, V>, kl: KeyList<K>?): Map<K, V> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            check !Map<K, V>::s_has_key(mp, k);
            return Map<K, V>::s_union[recursive](m, Map<K, V>::s_add(mp, k, v), kl.tail);
        }
    }

    hidden recursive static s_merge(m: Map<K, V>, mp: Map<K, V>, kl: KeyList<K>?): Map<K, V> {
        if(kl == none) {
            return mp;
        }
        else {
            let k = kl.hkey;
            let v = Map<K, V>::s_at_val(m, k);

            if(!Map<K, V>::s_has_key(mp, kl.hkey)) {
                return Map<K, V>::s_merge[recursive](m, Map<K, V>::s_add(mp, k, v), kl.tail);
            }
            else {
                return Map<K, V>::s_merge[recursive](m, Map<K, V>::s_update(mp, k, v), kl.tail);
            }
        }
    }

    hidden recursive static s_unionall(ml: List<Map<K, V>>, mlsize: Int, mp: Map<K, V>, idx: Int): Map<K, V> {
        if(idx == mlsize) {
            return mp;
        }
        else {
            let m = List<Map<K, V>>::s_unsafe_get(ml, idx);
            return Map<K, V>::s_unionall[recursive](ml, mlsize, mp.unionWith(m), idx + 1);
        }
    }

    hidden recursive static s_mergeall(ml: List<Map<K, V>>, mlsize: Int, mp: Map<K, V>, idx: Int): Map<K, V> {
        if(idx == mlsize) {
            return mp;
        }
        else {
            let m = List<Map<K, V>>::s_unsafe_get(ml, idx);
            return Map<K, V>::s_mergeall[recursive](ml, mlsize, mp.mergeWith(m), idx + 1);
        }
    }

    method size(): Int {
        return Map<K, V>::s_size(this);
    }

    method empty(): Bool {
        return Map<K, V>::s_size(this) == 0;
    }

    method keys(): List<K> {
        return Map<K, V>::s_key_list[recursive](KeyList<K>::sort(Map<K, V>::s_get_keylist(this)), List<K>@{});
    }

    method keySet(): Set<K> {
        return Map<K, V>::s_key_set(this);
    }

    method values(): List<V> {
        return Map<K, V>::s_value_list[recursive](this, KeyList<K>::sort(Map<K, V>::s_get_keylist(this)), List<V>@{});
    }

    method entries(): List<MapEntry<K, V>> {
        return Map<K, V>::s_entry_list[recursive](this, KeyList<K>::sort(Map<K, V>::s_get_keylist(this)), List<MapEntry<K, V>>@{});
    }

    method toDynamicMap(): DynamicMap<K, V> {
        abort;
    }

    method has(key: K): Bool {
        return Map<K, V>::s_has_key(this, key);
    }

    method hasAll(...kl: List<K>): Bool {
        return Map<K, V>::s_hasall[recursive](this, kl, List<K>::s_size(kl), 0);
    }

    method domainSubsetOf(s: Set<K>): Bool {
        if(Set<K>::s_size(s) > Map<K, V>::s_size(this)) {
            return false;
        }
        else {
            return Map<K, V>::s_domainincludes[recursive](this, Set<K>::s_get_keylist(s));
        }
    }

    method get(key: K): V 
        requires release Map<K, V>::s_has_key(this, key);
    {
        return Map<K, V>::s_at_val(this, key);
    }

    method tryGet(k: K): V? {
        return Map<K, V>::s_has_key(this, k) ? Map<K, V>::s_at_val(this, k) : none;
    }

    method defaultGet(k: K, default: V): V {
        return Map<K, V>::s_has_key(this, k) ? Map<K, V>::s_at_val(this, k) : default;
    }

    recursive? method submap(p: recursive? fn(_: K, _: V) -> Bool): Map<K, V> {
        return Map<K, V>::s_submap[recursive](this, Map<K, V>::s_get_keylist(this), Map<K, V>@{}, p);
    }

    method ofType<T where KeyType, U>(): Map<T, U> {
        return Map<K, V>::s_oftype<T, U>[recursive](this, Map<K, V>::s_get_keylist(this), Map<T, U>@{});
    }

    method cast<T where KeyType, U>(): Map<T, U> {
        return Map<K, V>::s_cast<T, U>[recursive](this, Map<K, V>::s_get_keylist(this), Map<T, U>@{});
    }

    method projectAll(...kl: List<K>): Map<K, V> {
        return Map<K, V>::s_projectall(this, kl);
    }

    method excludeAll(...kl: List<K>): Map<K, V> {
        return Map<K, V>::s_excludeall(this, kl);
    }

    method projectSet(ds: Set<K>): Map<K, V> {
        return Map<K, V>::s_project[recursive](this, Set<K>::s_get_keylist(ds), Map<K, V>@{});
    }

    method excludeSet(ds: Set<K>): Map<K, V> {
        return Map<K, V>::s_exclude[recursive](this, ds, Map<K, V>::s_get_keylist(this), Map<K, V>@{});
    }

    recursive? method remap<U>(f: recursive? fn(_: K, _: V) -> U): Map<K, U> {
        return Map<K, V>::s_remap<U>[recursive](this, Map<K, V>::s_get_keylist(this), Map<K, U>@{}, f);
    }

    method {when V KeyType} composeWith<U>(m: Map<V, U>): Map<K, U> {
        return Map<K, V>::s_composewith<U>[recursive](this, Map<K, V>::s_get_keylist(this), Map<K, U>@{}, m);
    }

    method {when V KeyType} tryComposeWith<U>(m: Map<V, U>): Map<K, U?> {
        return Map<K, V>::s_trycomposewith<U>[recursive](this, Map<K, V>::s_get_keylist(this), Map<K, U?>@{}, m);
    }

    method {when V KeyType} defaultComposeWith<U>(m: Map<V, U>, default: U): Map<K, U> {
        return Map<K, V>::s_defaultcomposewith<U>[recursive](this, Map<K, V>::s_get_keylist(this), Map<K, U>@{}, m, default);
    }

    method {when V KeyType} invertInjective(): Map<V, K> {
        return Map<K, V>::s_invertinj[recursive](this, Map<K, V>::s_get_keylist(this), Map<V, K>@{});
    }

    method {when V KeyType} invertRelational(): Map<V, List<K>> {
        return Map<K, V>::s_invertrel[recursive](this, KeyList<K>::sort(Map<K, V>::s_get_keylist(this)), Map<V, List<K>>@{});
    }

    method unionWith(m: Map<K, V>): Map<K, V> {
        return Map<K, V>::s_union[recursive](m, this, Map<K, V>::s_get_keylist(m));
    }

    static unionOf(...ml: List<Map<K, V>>): Map<K, V> {
        if(List<Map<K, V>>::s_size(ml) == 0) {
            return Map<K, V>@{};
        }
        elif(List<Map<K, V>>::s_size(ml) == 1) {
            return List<Map<K, V>>::s_unsafe_get(ml, 0);
        }
        else {
            return Map<K, V>::s_unionall[recursive](ml, List<Map<K, V>>::s_size(ml), Map<K, V>@{}, 0);
        }
    }

    method mergeWith(m: Map<K, V>): Map<K, V> {
        return Map<K, V>::s_merge[recursive](m, this, Map<K, V>::s_get_keylist(m));
    }

    static mergeOf(...ml: List<Map<K, V>>): Map<K, V> {
        if(List<Map<K, V>>::s_size(ml) == 0) {
            return Map<K, V>@{};
        }
        elif(List<Map<K, V>>::s_size(ml) == 1) {
            return List<Map<K, V>>::s_unsafe_get(ml, 0);
        }
        else {
            return Map<K, V>::s_mergeall[recursive](ml, List<Map<K, V>>::s_size(ml), Map<K, V>@{}, 0);
        }
    }
}

entity DynamicMap<K where KeyType, V> provides Object, Expandoable<MapEntry<K, V>> {
    method size(): Int {
        abort;
    }

    method empty(): Bool  {
        abort;
    }

    method keys(): List<K>  {
        abort;
    }

    method keySet(): Set<K>  {
        abort;
    }

    method values(): List<V>  {
        abort;
    }

    method entries(): List<MapEntry<K, V>>  {
        abort;
    }

    method toMap(): DynamicMap<K, V> {
        abort;
    }

    method has(key: K): Bool {
        abort;
    }

    method hasAll(...ks: List<K>): Bool {
        abort;
    }

    method domainSubsetOf(s: Set<K>): Bool {
        abort;
    }

    method get(key: K): V {
        abort;
    }

    method tryGet(k: K): V? {
        abort;
    }

    method add(k: K, v: V): DynamicMap<K, V> {
        abort;
    }

    method addAll(...el: List<MapEntry<K, V>>): DynamicMap<K, V> {
        abort;
    }

    method insert(k: K, v: V): DynamicMap<K, V> {
        abort;
    }

    method insertAll(...el: List<MapEntry<K, V>>): DynamicMap<K, V> {
        abort;
    }

    method update(k: K, v: V): DynamicMap<K, V> {
        abort;
    }

    method updateAll(...el: List<MapEntry<K, V>>): DynamicMap<K, V> {
        abort;
    }

    method remove(k: K): DynamicMap<K, V> {
        abort;
    }

    method removeAll(...kl: List<K>): DynamicMap<K, V> {
        abort;
    }

    recursive? method submap(p: recursive? fn(_: K, _: V) -> Bool): DynamicMap<K, V> {
        abort;
    }

    method ofType<T where KeyType, U>(): Map<T, U> {
        abort;
    }

    method cast<T where KeyType, U>(): Map<T, U> {
        abort;
    }

    method projectAll(...kl: List<K>): DynamicMap<K, V> {
        abort;
    }

    method excludeAll(...kl: List<K>): Map<K, V> {
        abort;
    }

    method projectSet(ds: Set<K>): Map<K, V> {
        abort;
    }

    method excludeSet(ds: Set<K>): Map<K, V> {
        abort;
    }

    recursive? method remap<U>(f: recursive? fn(_: K, _: V) -> U): DynamicMap<K, U> {
        abort;
    }

    method unionWith(m: Map<K, V>): DynamicMap<K, V> {
        abort;
    }

    static union(...ml: List<Map<K, V>>): DynamicMap<K, V> {
        abort;
    }

    method mergeWith(m: Map<K, V>): DynamicMap<K, V> {
        abort;
    }

    static merge(...ml: List<Map<K, V>>): DynamicMap<K, V> {
        abort;
    }
}


